/-
Copyright (c) 2023 Monica Omar. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Monica Omar
-/
import linear_algebra.matrix.pos_def
import linear_algebra.my_ips.pos
import linear_algebra.my_matrix.basic
import linear_algebra.End
import linear_algebra.my_ips.rank_one
import linear_algebra.matrix.nonsingular_inverse
import preq.ites
import preq.is_R_or_C_le

/-!

# the correspondence between matrix.pos_semidef and linear_map.is_positive

In this file, we show that
`x âˆˆ Mâ‚™` being positive semi-definite is equivalent to
`x.to_euclidean_lin.is_positive`

-/

-------------------------------
--copied from old mathlib
namespace matrix
 variables {ğ•œ m n : Type*} [fintype m] [decidable_eq m] [fintype n] [decidable_eq n] [is_R_or_C ğ•œ]
 open_locale complex_conjugate

 /-- The adjoint of the linear map associated to a matrix is the linear map associated to the
 conjugate transpose of that matrix. -/
 lemma conj_transpose_eq_adjoint (A : matrix m n ğ•œ) :
   to_lin' A.conj_transpose =
   @linear_map.adjoint ğ•œ (euclidean_space ğ•œ n) (euclidean_space ğ•œ m) _ _ _ _ _ _ _ (to_lin' A) :=
 begin
   rw @linear_map.eq_adjoint_iff _ (euclidean_space ğ•œ m) (euclidean_space ğ•œ n),
   intros x y,
   convert dot_product_assoc (conj âˆ˜ (id x : m â†’ ğ•œ)) y A using 1,
   simp [dot_product, mul_vec, ring_hom.map_sum,  â† star_ring_end_apply, mul_comm],
 end
end matrix
-------------------------------

variables {n ğ•œ : Type*} [is_R_or_C ğ•œ] [fintype n]

open_locale matrix

lemma matrix.pos_semidef.star_mul_self {n : Type*} [fintype n] (x : matrix n n ğ•œ) :
  (xá´´ â¬ x).pos_semidef :=
begin
  simp_rw [matrix.pos_semidef],
  split,
  { exact matrix.is_hermitian_transpose_mul_self _, },
  { intros y,
    have : is_R_or_C.re ((star y) â¬áµ¥ ((xá´´ â¬ x).mul_vec y))
      = is_R_or_C.re ((star (x.mul_vec y)) â¬áµ¥ (x.mul_vec y)),
    { simp only [matrix.star_mul_vec, matrix.dot_product_mul_vec, matrix.vec_mul_vec_mul], },
    rw [this],
    clear this,
    simp_rw [matrix.dot_product, map_sum],
    apply finset.sum_nonneg',
    intros i,
    simp_rw [pi.star_apply, is_R_or_C.star_def, â† is_R_or_C.inner_apply],
    exact inner_self_nonneg, },
end

lemma matrix.pos_semidef.mul_star_self (x : matrix n n ğ•œ) :
  (x â¬ xá´´).pos_semidef :=
begin
  simp_rw [matrix.pos_semidef],
  split,
  { exact matrix.is_hermitian_mul_conj_transpose_self _, },
  { intros y,
    have : is_R_or_C.re ((star y) â¬áµ¥ ((x â¬ xá´´).mul_vec y))
      = is_R_or_C.re ((star ((xá´´).mul_vec y)) â¬áµ¥ ((xá´´).mul_vec y)),
    { simp_rw [matrix.star_mul_vec, matrix.dot_product_mul_vec,
        matrix.conj_transpose_conj_transpose, matrix.vec_mul_vec_mul], },
    rw [this],
    clear this,
    simp_rw [matrix.dot_product, map_sum],
    apply finset.sum_nonneg',
    intros i,
    simp_rw [pi.star_apply, is_R_or_C.star_def, â† is_R_or_C.inner_apply],
    exact inner_self_nonneg, },
end

lemma matrix.to_euclidean_lin_eq_pi_Lp_linear_equiv [decidable_eq n] (x : matrix n n ğ•œ) :
  x.to_euclidean_lin = ((pi_Lp.linear_equiv 2 ğ•œ (Î» _ : n, ğ•œ)).symm.conj
    x.to_lin' : module.End ğ•œ (pi_Lp 2 _)) :=
rfl

lemma matrix.pos_semidef_eq_linear_map_positive [decidable_eq n] (x : matrix n n ğ•œ) :
  (x.pos_semidef) â†”  x.to_euclidean_lin.is_positive :=
begin
  simp_rw [linear_map.is_positive, â† matrix.is_hermitian_iff_is_symmetric, matrix.pos_semidef, matrix.to_euclidean_lin_eq_pi_Lp_linear_equiv,
    pi_Lp.inner_apply, is_R_or_C.inner_apply, map_sum, linear_equiv.conj_apply,
    linear_map.comp_apply, linear_equiv.coe_coe, pi_Lp.linear_equiv_symm_apply,
    linear_equiv.symm_symm, pi_Lp.linear_equiv_apply, matrix.to_lin'_apply,
    pi_Lp.equiv_symm_apply, â† is_R_or_C.star_def, matrix.mul_vec, matrix.dot_product,
    pi_Lp.equiv_apply, â† pi.mul_apply (x _) _, â† matrix.dot_product, map_sum,
    pi.star_apply, matrix.mul_vec, matrix.dot_product, pi.mul_apply],
  exact âŸ¨Î» h, h, Î» h, hâŸ©,
end

lemma matrix.pos_semidef_iff [decidable_eq n] (x : matrix n n ğ•œ) :
  x.pos_semidef â†” âˆƒ y : matrix n n ğ•œ, x = yá´´ â¬ y :=
begin
  have : finite_dimensional.finrank ğ•œ (pi_Lp 2 (Î» (_x : n), ğ•œ)) = fintype.card n,
  { simp_rw [finrank_euclidean_space], },
  simp_rw [matrix.pos_semidef_eq_linear_map_positive,
    linear_map.is_positive_iff_exists_adjoint_mul_self _ this,
    matrix.to_euclidean_lin_eq_pi_Lp_linear_equiv,
    linear_equiv.conj_apply, linear_map.ext_iff, linear_map.comp_apply,
    linear_equiv.coe_coe, pi_Lp.linear_equiv_symm_apply, linear_equiv.symm_symm,
    pi_Lp.linear_equiv_apply, matrix.to_lin'_apply, function.funext_iff,
    pi_Lp.equiv_symm_apply, matrix.mul_vec, matrix.dot_product, pi_Lp.equiv_apply,
    matrix.ext_iff, matrix.mul_vec_eq, â† matrix.to_lin'_apply, matrix.to_lin'_mul,
    matrix.conj_transpose_eq_adjoint, matrix.to_lin'_apply, function.funext_iff,
    matrix.mul_vec, matrix.dot_product, linear_map.mul_eq_comp, linear_map.comp_apply],
  split,
  { rintros âŸ¨S, hSâŸ©,
    use S.to_matrix',
    intros c a,
    simp_rw [matrix.to_lin'_to_matrix', hS], },
  { rintros âŸ¨y, hyâŸ©,
    use y.to_lin',
    intros c a,
    exact hy c a, },
end

local notation `âŸª` x `,` y `âŸ«_ğ•œ` := @inner ğ•œ _ _ x y

open_locale big_operators
lemma matrix.dot_product_eq_inner {n : Type*} [fintype n] (x y : n â†’ ğ•œ) :
  matrix.dot_product (star x) y = âˆ‘ i : n, âŸªx i, y iâŸ«_ğ•œ :=
rfl

lemma matrix.pos_semidef.invertible_iff_pos_def {n : Type*} [fintype n] [decidable_eq n]
  {x : matrix n n ğ•œ} (hx : x.pos_semidef) :
  function.bijective x.to_lin' â†” x.pos_def :=
begin
  simp_rw [matrix.pos_def, hx.1, true_and],
  cases (matrix.pos_semidef_iff x).mp hx with y hy,
  split,
  { intros h v hv,
    rw hy,
    have : is_R_or_C.re ((star v) â¬áµ¥ ((yá´´ â¬ y).mul_vec v))
      = is_R_or_C.re ((star (y.mul_vec v)) â¬áµ¥ (y.mul_vec v)),
    { simp_rw [matrix.star_mul_vec, matrix.dot_product_mul_vec,
        matrix.vec_mul_vec_mul], },
    simp_rw [this, matrix.dot_product, map_sum, pi.star_apply, is_R_or_C.star_def,
      â† is_R_or_C.inner_apply, inner_self_eq_norm_sq_to_K],
    clear this,
    apply finset.sum_pos',
    { simp_rw [finset.mem_univ, forall_true_left],
      intros i,
      norm_cast,
      exact pow_two_nonneg _, },
    { simp_rw [finset.mem_univ, exists_true_left],
      suffices : y.mul_vec v â‰  0,
      { simp_rw [ne.def, function.funext_iff, pi.zero_apply] at this,
        push_neg at this,
        cases this with j hj,
        rw â† norm_ne_zero_iff at hj,
        use j,
        norm_cast,
        exact (sq_pos_iff _).mpr hj, },
      by_contra',
      apply hv,
      apply_fun x.to_lin',
      { simp_rw [map_zero],
        rw matrix.mul_vec_eq at hy,
        specialize hy v,
        simp_rw [â† matrix.mul_vec_mul_vec, this, matrix.mul_vec_zero] at hy,
        rw matrix.to_lin'_apply,
        exact hy, },
      { exact h.1, }, }, },
  { intros h,
    by_contra',
    rw [function.bijective, â† linear_map.injective_iff_surjective, and_self,
        injective_iff_map_eq_zero] at this,
    push_neg at this,
    cases this with a ha,
    specialize h a ha.2,
    rw [â† matrix.to_lin'_apply, ha.1, matrix.dot_product_zero, is_R_or_C.zero_re', lt_self_iff_false] at h,
    exact h, },
end

lemma matrix.is_hermitian_self_mul_conj_transpose (A : matrix n n ğ•œ) :
  (A.conj_transpose.mul A).is_hermitian :=
by rw [matrix.is_hermitian, matrix.conj_transpose_mul, matrix.conj_transpose_conj_transpose]

lemma matrix.trace_star {A : matrix n n ğ•œ} :
  star A.trace = Aá´´.trace :=
begin
  simp_rw [matrix.trace, matrix.diag, star_sum, matrix.conj_transpose_apply],
end

lemma matrix.nonneg_eigenvalues_of_pos_semidef
  [decidable_eq n] {Î¼ : â„} {A : matrix n n ğ•œ}
  (hÎ¼ : module.End.has_eigenvalue A.to_euclidean_lin â†‘Î¼)
  (H : A.pos_semidef) : 0 â‰¤ Î¼ :=
begin
  apply eigenvalue_nonneg_of_nonneg hÎ¼,
  simp_rw [matrix.to_euclidean_lin_eq_to_lin, matrix.to_lin_apply,
           inner_sum, inner_smul_right, pi_Lp.basis_fun_apply,
           pi_Lp.equiv_symm_single, euclidean_space.inner_single_right, one_mul],
  intros x,
  have : âˆ‘ (x_1 : n), A.mul_vec (((pi_Lp.basis_fun 2 ğ•œ n).repr) x) x_1 * (star_ring_end ((Î» (i : n), ğ•œ) x_1)) (x x_1) = matrix.dot_product (star x) (A.mul_vec x),
  { simp_rw [mul_comm, matrix.dot_product],
    congr, },
  rw this,
  exact H.2 _,
end

lemma matrix.is_hermitian.nonneg_eigenvalues_of_pos_semidef [decidable_eq n]
  [decidable_eq ğ•œ] {A : matrix n n ğ•œ} (hA : A.pos_semidef) (i : n) :
  0 â‰¤ hA.1.eigenvalues i :=
matrix.nonneg_eigenvalues_of_pos_semidef (hA.1.eigenvalues_has_eigenvalue _) hA

@[instance] noncomputable def matrix.pos_def.invertible [decidable_eq n] {Q : matrix n n ğ•œ}
  (hQ : Q.pos_def) : invertible Q :=
begin
  suffices : function.bijective Q.to_lin',
  { have h : invertible Q.to_lin',
    { refine is_unit.invertible _,
      rw linear_map.is_unit_iff_ker_eq_bot,
      exact linear_map.ker_eq_bot_of_injective this.1, },
    refine is_unit.invertible _,
    rw matrix.is_unit_iff_is_unit_det,
    rw â† linear_map.det_to_lin',
    apply linear_map.is_unit_det,
    rw â† nonempty_invertible_iff_is_unit,
    exact nonempty.intro h, },
  rw [matrix.pos_semidef.invertible_iff_pos_def hQ.pos_semidef],
  exact hQ,
end

lemma matrix.mul_vec_sum {n : Type*} [fintype n] (x : matrix n n ğ•œ) (y : n â†’ (n â†’ ğ•œ)):
  x.mul_vec (âˆ‘ i : n, y i) = âˆ‘ i : n, x.mul_vec (y i) :=
begin
  ext1,
  simp only [finset.sum_apply, matrix.mul_vec, matrix.dot_product, finset.mul_sum],
  rw finset.sum_comm,
end

open matrix
open_locale matrix

variables {E : Type*} [normed_add_comm_group E]

lemma matrix.to_lin_pi_Lp_eq_to_lin' {n : Type*} [fintype n] [decidable_eq n] :
  to_lin (pi_Lp.basis_fun 2 ğ•œ n) (pi_Lp.basis_fun 2 ğ•œ n) = to_lin' :=
rfl

lemma matrix.pos_semidef_iff_eq_rank_one {n : â„•} [decidable_eq ğ•œ] {x : matrix (fin n) (fin n) ğ•œ} :
  x.pos_semidef â†” âˆƒ (m : â„•) (v : fin m â†’ euclidean_space ğ•œ (fin n)),
    x = âˆ‘ i : fin m, (((rank_one (v i) (v i)
      : euclidean_space ğ•œ (fin n) â†’L[ğ•œ] euclidean_space ğ•œ (fin n))
      : euclidean_space ğ•œ (fin n) â†’â‚—[ğ•œ] euclidean_space ğ•œ (fin n))).to_matrix' :=
begin
  have : finite_dimensional.finrank ğ•œ (euclidean_space ğ•œ (fin n)) = n :=
  finrank_euclidean_space_fin,
  simp_rw [pos_semidef_eq_linear_map_positive, linear_map.is_positive_iff_eq_sum_rank_one this,
    to_euclidean_lin_eq_to_lin, matrix.to_lin_pi_Lp_eq_to_lin', â† map_sum],
  split; rintros âŸ¨m, y, hyâŸ©; refine âŸ¨m, y, _âŸ©,
  { simp_rw [â† hy, linear_map.to_matrix'_to_lin'], },
  { rw [hy, to_lin'_to_matrix'], },
end

/-- a matrix $x$ is positive semi-definite if and only if there exists vectors $(v_i)$ such that
  $\sum_i v_iv_i^*=x$  -/
lemma matrix.pos_semidef_iff_col_mul_conj_transpose_col [decidable_eq n] [decidable_eq ğ•œ]
  (x : matrix n n ğ•œ) :
  x.pos_semidef â†” âˆƒ (v : n â†’ (n â†’ ğ•œ)),
    âˆ‘ (i : n), col (v i) â¬ (col (v i))á´´ = x :=
begin
  simp_rw [conj_transpose_col],
  split,
  { intros hx,
    simp_rw [mul_vec_eq],
    let a : matrix n n ğ•œ :=
    Î» i j, (real.sqrt (hx.1.eigenvalues i)) â€¢ (hx.1.eigenvector_basis i) j,
    use a,
    intros u,
    ext1 j,
    nth_rewrite_rhs 0 â† continuous_linear_map.one_apply u,
    rw â† euclidean_space.rank_one_of_orthonormal_basis_eq_one hx.1.eigenvector_basis,
    simp_rw [continuous_linear_map.sum_apply, rank_one_apply, mul_vec_sum, mul_vec_smul,
      hx.1.apply_eigenvector_basis],
    simp only [mul_vec, dot_product, finset.sum_apply, finset.sum_mul, finset.sum_smul,
      finset.sum_apply, pi.smul_apply, mul_apply, col_apply, row_apply, smul_eq_mul,
      mul_smul_comm, euclidean_space.inner_eq],
    rw finset.sum_comm,
    apply finset.sum_congr rfl, intros,
    apply finset.sum_congr rfl, intros,
    simp only [fintype.univ_punit, finset.sum_const, finset.card_singleton, nsmul_eq_mul,
        algebra_map.coe_one, one_mul],
    simp_rw [pi.star_apply, a, is_R_or_C.real_smul_eq_coe_mul, star_mul',
      is_R_or_C.star_def, is_R_or_C.conj_of_real, mul_mul_mul_comm, â† is_R_or_C.of_real_mul,
      â† real.sqrt_mul (nonneg_eigenvalues_of_pos_semidef (hx.1.eigenvalues_has_eigenvalue _) hx),
      â† pow_two, real.sqrt_sq (nonneg_eigenvalues_of_pos_semidef
        (hx.1.eigenvalues_has_eigenvalue _) hx)],
    ring_nf, },
  { rintros âŸ¨v, hvâŸ©,
    rw [pos_semidef],
    have : x.is_hermitian,
    { simp_rw [is_hermitian, â† hv, conj_transpose_sum, conj_transpose_mul, conj_transpose_row,
      conj_transpose_col, star_star], },
    { refine âŸ¨this, _âŸ©,
      intros y,
      have : âˆ€ x, v x â¬áµ¥ star y = star (y â¬áµ¥ star (v x)),
      { intros, simp_rw [â† star_dot_product_star, star_star], },
      simp_rw [â† trace_col_mul_row, row_mul_vec, transpose_mul, transpose_col, â† matrix.mul_assoc,
        â† hv, transpose_sum, matrix.mul_sum, transpose_mul, transpose_col, transpose_row,
        trace_sum, map_sum, â† matrix.mul_assoc, trace_mul_comm _ (row (v _)), â† matrix.mul_assoc,
        matrix.trace, diag, matrix.mul_assoc _ (row y),
        @mul_apply _ _ _ _ _ _ _ ((row (v _)) â¬ (col (star y))) _, row_mul_col_apply,
        this],
      simp only [fintype.univ_punit, finset.sum_const, finset.card_singleton, nsmul_eq_mul,
        algebra_map.coe_one, one_mul, is_R_or_C.star_def, is_R_or_C.conj_mul],
      apply finset.sum_nonneg,
      intros,
      norm_cast,
      exact is_R_or_C.norm_sq_nonneg _, }, },
end

lemma vec_mul_vec.pos_semidef [decidable_eq n] [decidable_eq ğ•œ] (x : n â†’ ğ•œ) :
  (vec_mul_vec x (star x)).pos_semidef :=
begin
  rw matrix.pos_semidef_iff_col_mul_conj_transpose_col,
  by_cases nonempty n,
  { let i : n := nonempty.some h,
    let v : n â†’ n â†’ ğ•œ := Î» j k, ite (i = j) (x k) 0,
    use v,
    simp_rw [v, â† matrix.ext_iff, matrix.sum_apply, mul_apply, col_apply,
      conj_transpose_apply, col_apply, star_ite, star_zero, ite_mul, zero_mul,
      mul_ite, mul_zero, finset.sum_ite_irrel, finset.sum_const_zero,
      finset.sum_ite_eq, finset.mem_univ, if_true, eq_self_iff_true,
      if_true, finset.sum_const],
    intros,
    simp only [fintype.univ_punit, finset.card_singleton, is_R_or_C.star_def,
      nsmul_eq_mul, algebra_map.coe_one, one_mul],
    simp_rw [vec_mul_vec_apply, pi.star_apply, is_R_or_C.star_def], },
  { rw not_nonempty_iff at h,
    haveI : is_empty n := h,
    simp only [eq_iff_true_of_subsingleton, exists_const], },
end

variables {Mâ‚ Mâ‚‚ : Type*} [normed_add_comm_group Mâ‚] [normed_add_comm_group Mâ‚‚]
  [inner_product_space â„‚ Mâ‚] [inner_product_space â„‚ Mâ‚‚]

/-- we say a linear map $T \colon L(M_1) \to L(M_2)$ is a positive map
  if for all positive $x \in L(M_1)$, we also get $T(x)$ is positive  -/
def linear_map.positive_map (T : (Mâ‚ â†’â‚—[â„‚] Mâ‚) â†’â‚—[â„‚] (Mâ‚‚ â†’â‚—[â„‚] Mâ‚‚)) :
  Prop :=
âˆ€ x : Mâ‚ â†’â‚—[â„‚] Mâ‚, x.is_positive â†’ (T x).is_positive

/-- a $^*$-homomorphism from $L(M_1)$ to $L(M_2)$ is a positive map -/
lemma linear_map.positive_map.star_hom {nâ‚ : â„•}
  [finite_dimensional â„‚ Mâ‚] [finite_dimensional â„‚ Mâ‚‚]
  (hnâ‚ : finite_dimensional.finrank â„‚ Mâ‚ = nâ‚)
  (Ï† : star_alg_hom â„‚ (Mâ‚ â†’â‚—[â„‚] Mâ‚) (Mâ‚‚ â†’â‚—[â„‚] Mâ‚‚)) :
  Ï†.to_alg_hom.to_linear_map.positive_map :=
begin
  intros x hx,
  rcases (linear_map.is_positive_iff_exists_adjoint_mul_self x hnâ‚).mp hx with âŸ¨w, rflâŸ©,
  have : âˆ€ h, Ï†.to_alg_hom.to_linear_map h = Ï† h := Î» h, rfl,
  simp_rw [linear_map.is_positive, linear_map.is_symmetric, this, _root_.map_mul,
    â† linear_map.star_eq_adjoint, map_star, linear_map.mul_apply, linear_map.star_eq_adjoint,
    linear_map.adjoint_inner_left, linear_map.adjoint_inner_right, eq_self_iff_true,
    forall_2_true_iff, true_and, inner_self_nonneg, forall_const],
end

/-- the identity is a positive definite matrix -/
lemma matrix.pos_def_one [decidable_eq n] :
  (1 : matrix n n ğ•œ).pos_def :=
begin
  simp_rw [matrix.pos_def, matrix.is_hermitian, matrix.conj_transpose_one,
           eq_self_iff_true, true_and, matrix.one_mul_vec,
           matrix.dot_product_eq_inner, â† matrix.vec_ne_zero, map_sum],
  intros x h,
  apply finset.sum_pos',
  simp only [finset.mem_univ, forall_true_left],
  intros i,
  exact inner_self_nonneg,
  cases h with i hi,
  use i,
  simp_rw [finset.mem_univ, true_and],
  simp_rw [ne.def] at hi,
  contrapose! hi,
  rw inner_self_nonpos at hi,
  exact hi,
end

/-- each eigenvalue of a positive definite matrix is positive -/
lemma matrix.pos_def.pos_eigenvalues [decidable_eq ğ•œ] [decidable_eq n]
  {A : matrix n n ğ•œ} (hA : A.pos_def) (i : n) :
  0 < hA.is_hermitian.eigenvalues i :=
begin
  letI := hA,
  have := matrix.nonneg_eigenvalues_of_pos_semidef
    (matrix.is_hermitian.eigenvalues_has_eigenvalue hA.1 i)
    (matrix.pos_def.pos_semidef hA),
  suffices t1 : 0 â‰  hA.is_hermitian.eigenvalues i,
  { contrapose t1,
    push_neg,
    exact eq_iff_le_not_lt.mpr (and.intro this t1), },
  have : function.injective A.to_lin' :=
  ((matrix.pos_semidef.invertible_iff_pos_def hA.pos_semidef).mpr hA).1,
  have : function.injective A.to_euclidean_lin,
  { rw matrix.to_euclidean_lin_eq_pi_Lp_linear_equiv,
    exact this, },
  cases module.End.has_eigenvector_iff_has_eigenvalue.mpr
    (matrix.is_hermitian.eigenvalues_has_eigenvalue hA.1 i) with v hv,
  intros h,
  simp_rw [â† h, is_R_or_C.of_real_zero, zero_smul] at hv,
  exact ((map_ne_zero_iff _ this).mpr hv.2) hv.1,
end

lemma matrix.pos_def.pos_trace [decidable_eq n] [decidable_eq ğ•œ]
  [nonempty n] {x : matrix n n ğ•œ} (hx : x.pos_def) :
  0 < is_R_or_C.re x.trace :=
begin
  simp_rw [is_hermitian.trace_eq hx.1, map_sum, is_R_or_C.of_real_re],
  apply finset.sum_pos,
  { exact Î» _ _, hx.pos_eigenvalues _, },
  { exact finset.univ_nonempty, },
end

/-- the trace of a positive definite matrix is non-zero -/
lemma matrix.pos_def.trace_ne_zero [decidable_eq n] [nonempty n]
  [decidable_eq ğ•œ] {x : matrix n n ğ•œ} (hx : x.pos_def) :
  x.trace â‰  0 :=
begin
  rw matrix.is_hermitian.trace_eq hx.is_hermitian,
  norm_num,
  rw [â† is_R_or_C.of_real_sum, is_R_or_C.of_real_eq_zero,
    finset.sum_eq_zero_iff_of_nonneg _],
  { simp_rw [finset.mem_univ, true_implies_iff],
    simp only [not_forall],
    use _inst_9.some,
    exact norm_num.ne_zero_of_pos _
      (matrix.pos_def.pos_eigenvalues hx _), },
  { intros,
    exact le_of_lt (matrix.pos_def.pos_eigenvalues hx _), },
end

open_locale complex_order
lemma pos_semidef.complex [decidable_eq n] (x : matrix n n â„‚) :
  x.pos_semidef â†” âˆ€ y : n â†’ â„‚,
    0 â‰¤ (star y â¬áµ¥ x.mul_vec y) :=
    -- ((star y â¬áµ¥ x.mul_vec y).re : â„‚) = star y â¬áµ¥ x.mul_vec y âˆ§ 0 â‰¤ (star y â¬áµ¥ x.mul_vec y).re :=
begin
  simp_rw [pos_semidef_eq_linear_map_positive x, linear_map.complex_is_positive,
    is_R_or_C.nonneg_def'],
  refl,
end

lemma std_basis_matrix.sum_eq_one [decidable_eq n] (a : ğ•œ) :
  âˆ‘ k : n, std_basis_matrix k k a = a â€¢ 1 :=
by simp_rw [â† matrix.ext_iff, matrix.sum_apply, pi.smul_apply,
  std_basis_matrix, one_apply, smul_ite, ite_and, finset.sum_ite_eq',
  finset.mem_univ, if_true, smul_eq_mul, mul_zero, mul_one, eq_self_iff_true,
  forall_2_true_iff]

lemma std_basis_matrix_mul [decidable_eq n] (i j k l : n) (a b : ğ•œ) :
  std_basis_matrix i j a â¬ std_basis_matrix k l b
    = ite (j = k) (1 : ğ•œ) (0 : ğ•œ) â€¢ std_basis_matrix i l (a * b) :=
begin
  ext1,
  simp_rw [mul_apply, std_basis_matrix, ite_mul, zero_mul, mul_ite, mul_zero, pi.smul_apply,
    ite_and, finset.sum_ite_irrel, finset.sum_const_zero, finset.sum_ite_eq, finset.mem_univ,
    if_true, â† ite_and, â† and_assoc, ite_smul, zero_smul, one_smul, â† ite_and, â† and_assoc,
    and_comm (j = k), eq_comm],
end

lemma matrix.smul_std_basis_matrix' {n R : Type*} [comm_semiring R] [decidable_eq n] (i j : n) (c : R) :
  std_basis_matrix i j c = c â€¢ std_basis_matrix i j 1 :=
begin
  rw [smul_std_basis_matrix, smul_eq_mul, mul_one],
end

lemma matrix.trace_iff' (x : matrix n n ğ•œ) :
  x.trace = âˆ‘ i : n, x i i :=
rfl

lemma exists_unique_trace [decidable_eq n] [nontrivial n] :
  âˆƒ! Ï† : matrix n n ğ•œ â†’â‚—[ğ•œ] ğ•œ, (âˆ€ a b : matrix n n ğ•œ, Ï† (a â¬ b) = Ï† (b â¬ a)) âˆ§ Ï† 1 = 1 :=
begin
  use (1 / fintype.card n : ğ•œ) â€¢ trace_linear_map n ğ•œ ğ•œ,
  have trace_functional_iff : âˆ€ (Ï† : matrix n n ğ•œ â†’â‚—[ğ•œ] ğ•œ),
    ((âˆ€ a b : matrix n n ğ•œ, Ï† (a â¬ b) = Ï† (b â¬ a)) âˆ§ (Ï† (1) = 1))
    â†” Ï† = (1 / fintype.card n : ğ•œ) â€¢ trace_linear_map n ğ•œ ğ•œ :=
  begin
    intros,
    have : (â†‘(fintype.card n) : ğ•œ)â»Â¹ * â†‘(finset.univ.card) = 1,
    { rw inv_mul_eq_oneâ‚€,
      { refl, },
      { simp only [ne.def, nat.cast_eq_zero, fintype.card_ne_zero],
        exact not_false, }, },
    split,
    { intros h,
      rw linear_map.ext_iff,
      intros x,
      have : âˆ€ i j : n, Ï† (std_basis_matrix i j (1 : ğ•œ))
        = (1 / (fintype.card n : ğ•œ)) â€¢ ite (j = i) (1 : ğ•œ) (0 : ğ•œ),
      { intros i j,
        calc Ï† (std_basis_matrix i j (1 : ğ•œ))
          = (1 / (fintype.card n : ğ•œ))
            â€¢ âˆ‘ k, Ï† (std_basis_matrix i k 1 â¬ (std_basis_matrix k j 1 : matrix n n ğ•œ)) : _
          ... = (1 / (fintype.card n : ğ•œ))
            â€¢ âˆ‘ k, Ï† (std_basis_matrix k j 1 â¬ std_basis_matrix i k 1) : _
          ... = (1 / (fintype.card n : ğ•œ)) â€¢ ite (j = i) (Ï† (âˆ‘ k, std_basis_matrix k k 1)) 0 : _
          ... = (1 / (fintype.card n : ğ•œ)) â€¢ ite (j = i) (Ï† 1) 0 : _
          ... = (1 / (fintype.card n : ğ•œ)) â€¢ ite (j = i) 1 0 : _,
        { simp_rw [std_basis_matrix.mul_same, one_mul],
          simp only [one_div, finset.sum_const, nsmul_eq_mul, algebra.id.smul_eq_mul],
          rw [â† mul_assoc],
          simp_rw [this, one_mul], },
        { simp_rw h.1, },
        { simp_rw [std_basis_matrix_mul, one_mul, smul_hom_class.map_smul,
            smul_eq_mul, boole_mul, finset.sum_ite_irrel, finset.sum_const_zero,
            map_sum], },
        { simp_rw [std_basis_matrix.sum_eq_one, one_smul], },
        { simp_rw h.2, }, },
      rw [linear_map.smul_apply, trace_linear_map_apply],
      nth_rewrite_lhs 0 matrix_eq_sum_std_basis x,
      simp_rw [matrix.smul_std_basis_matrix' _ _ (x _ _), map_sum, smul_hom_class.map_smul],
      calc âˆ‘ x_1 x_2, x x_1 x_2 â€¢ Ï† (std_basis_matrix x_1 x_2 1)
        = âˆ‘ x_1 x_2, x x_1 x_2 â€¢ (1 / (fintype.card n : ğ•œ)) â€¢ ite (x_2 = x_1) (1 : ğ•œ) 0 : _
        ... = âˆ‘ x_1, x x_1 x_1 â€¢ (1 / fintype.card n : ğ•œ) : _
        ... = (1 / fintype.card n : ğ•œ) â€¢ x.trace : _,
      { simp_rw â† this, },
      { simp_rw [smul_ite, smul_zero, finset.sum_ite_eq', finset.mem_univ, if_true,
          smul_eq_mul, mul_one], },
      { simp_rw [â† finset.sum_smul, matrix.trace_iff' x, smul_eq_mul, mul_comm], }, },
    { rintros rfl,
      simp_rw [linear_map.smul_apply, trace_linear_map_apply, matrix.trace_iff' 1, one_apply_eq,
        finset.sum_const, one_div, nsmul_eq_mul, mul_one],
      refine âŸ¨Î» x y, _, thisâŸ©,
      rw trace_mul_comm, },
  end,
  simp only [trace_functional_iff, imp_self, forall_true_iff, and_true],
end

lemma matrix.std_basis_matrix.trace [decidable_eq n] (i j : n) (a : ğ•œ) :
  (std_basis_matrix i j a).trace = ite (i = j) a 0 :=
by simp_rw [matrix.trace_iff', std_basis_matrix, ite_and, finset.sum_ite_eq, finset.mem_univ,
  if_true, eq_comm]

lemma matrix.std_basis_matrix_eq {n : Type*} [decidable_eq n] (i j : n) (a : ğ•œ) :
  std_basis_matrix i j a = Î» (i' j' : n), ite (i = i' âˆ§ j = j') a 0 :=
rfl

lemma vec_mul_vec_eq_zero_iff (x : n â†’ ğ•œ) :
  vec_mul_vec x (star x) = 0 â†” x = 0 :=
begin
  simp_rw [â† matrix.ext_iff, vec_mul_vec_apply, dmatrix.zero_apply,
    pi.star_apply, mul_comm _ (star _), function.funext_iff, pi.zero_apply],
  split,
  { intros h i,
    specialize h i i,
    rw [is_R_or_C.star_def, is_R_or_C.conj_mul, is_R_or_C.of_real_eq_zero,
      is_R_or_C.norm_sq_eq_zero] at h,
    exact h, },
  { intros h i j,
    simp_rw [h, mul_zero], },
end

lemma matrix.pos_def.diagonal [decidable_eq n] (x : n â†’ ğ•œ) :
  (diagonal x).pos_def â†” âˆ€ i, 0 < is_R_or_C.re (x i) âˆ§ (is_R_or_C.re (x i) : ğ•œ) = x i :=
begin
  split,
  { intros h i,
    have h' := h.2,
    simp only [dot_product, mul_vec, diagonal, ite_mul, of_apply, zero_mul, finset.sum_ite_eq,
      finset.mem_univ, if_true] at h',
    let g : n â†’ ğ•œ := Î» p, ite (i = p) 1 0,
    have : g â‰  0,
    { rw [ne.def, function.funext_iff, not_forall],
      simp_rw [pi.zero_apply],
      use i,
      simp_rw [g, eq_self_iff_true, if_true],
      exact one_ne_zero, },
    specialize h' g this,
    simp_rw [mul_rotate', pi.star_apply, g, star_ite, star_zero, star_one, boole_mul, â† ite_and,
      and_self, mul_boole, finset.sum_ite_eq, finset.mem_univ, if_true] at h',
    split,
    { exact h', },
    { have := h.1,
      simp_rw [is_hermitian, diagonal_conj_transpose, diagonal_eq_diagonal_iff,
        pi.star_apply, is_R_or_C.star_def, is_R_or_C.conj_eq_iff_re] at this,
      exact this i, }, },
  { intros h,
    split,
    { simp_rw [is_hermitian, diagonal_conj_transpose, diagonal_eq_diagonal_iff, pi.star_apply,
        is_R_or_C.star_def, is_R_or_C.conj_eq_iff_re],
      intros,
      exact (h _).2, },
    { intros y hy,
      simp only [mul_vec, dot_product_diagonal, dot_product, diagonal, ite_mul, zero_mul, mul_ite,
        mul_zero, of_apply, finset.sum_ite_eq, finset.mem_univ, if_true],
      simp_rw [pi.star_apply, mul_rotate' (star (y _)), mul_comm (y _),
        is_R_or_C.star_def, is_R_or_C.conj_mul, map_sum, mul_comm (x _),
        is_R_or_C.of_real_mul_re],
      apply finset.sum_pos',
      intros,
      exact mul_nonneg (is_R_or_C.norm_sq_nonneg _) (le_of_lt (h _).1),
      { simp_rw [ne.def, function.funext_iff, pi.zero_apply, not_forall] at hy,
        obtain âŸ¨i,hiâŸ© := hy,
        exact âŸ¨i, finset.mem_univ _, mul_pos (is_R_or_C.norm_sq_pos.mpr hi) (h _).1âŸ©, }, }, },
end

lemma matrix.pos_semidef.diagonal [decidable_eq n] (x : n â†’ ğ•œ) :
  (diagonal x).pos_semidef â†” âˆ€ i, 0 â‰¤ is_R_or_C.re (x i) âˆ§ (is_R_or_C.re (x i) : ğ•œ) = x i :=
begin
  split,
  { intros h i,
    have h' := h.2,
    simp only [dot_product, mul_vec, diagonal, ite_mul, of_apply, zero_mul, finset.sum_ite_eq,
      finset.mem_univ, if_true] at h',
    let g : n â†’ ğ•œ := Î» p, ite (i = p) 1 0,
    specialize h' g,
    simp_rw [mul_rotate', pi.star_apply, g, star_ite, star_zero, star_one, boole_mul, â† ite_and,
      and_self, mul_boole, finset.sum_ite_eq, finset.mem_univ, if_true] at h',
    split,
    { exact h', },
    { have := h.1,
      simp_rw [is_hermitian, diagonal_conj_transpose, diagonal_eq_diagonal_iff,
        pi.star_apply, is_R_or_C.star_def, is_R_or_C.conj_eq_iff_re] at this,
      exact this i, }, },
  { intros h,
    split,
    { simp_rw [is_hermitian, diagonal_conj_transpose, diagonal_eq_diagonal_iff, pi.star_apply,
        is_R_or_C.star_def, is_R_or_C.conj_eq_iff_re],
      intros,
      exact (h _).2, },
    { intros y,
      simp only [mul_vec, dot_product_diagonal, dot_product, diagonal, ite_mul, zero_mul, mul_ite,
        mul_zero, of_apply, finset.sum_ite_eq, finset.mem_univ, if_true],
      simp_rw [pi.star_apply, mul_rotate' (star (y _)), mul_comm (y _),
        is_R_or_C.star_def, is_R_or_C.conj_mul, map_sum, mul_comm (x _),
        is_R_or_C.of_real_mul_re],
      apply finset.sum_nonneg',
      intros,
      exact mul_nonneg (is_R_or_C.norm_sq_nonneg _) (h _).1, }, },
end

namespace matrix

lemma trace_conj_transpose_mul_self_re_nonneg (x : matrix n n ğ•œ) :
  0 â‰¤ is_R_or_C.re (xá´´ â¬ x).trace :=
begin
  simp_rw [matrix.trace, matrix.diag, map_sum, matrix.mul_apply, matrix.conj_transpose_apply,
    map_sum],
  apply finset.sum_nonneg',
  intros i,
  apply finset.sum_nonneg',
  intros j,
  simp_rw [is_R_or_C.star_def, â† is_R_or_C.inner_apply],
  exact inner_self_nonneg,
end

lemma pos_semidef.trace_conj_transpose_mul_self_nonneg [decidable_eq n]
  {Q : matrix n n ğ•œ} (x : matrix n n ğ•œ) (hQ : Q.pos_semidef) :
  (is_R_or_C.re (Q â¬ xá´´ â¬ x).trace : ğ•œ) = (Q â¬ xá´´ â¬ x).trace
    âˆ§ 0 â‰¤ is_R_or_C.re (Q â¬ xá´´ â¬ x).trace :=
begin
  rcases (matrix.pos_semidef_iff Q).mp hQ with âŸ¨y, rflâŸ©,
  rw [matrix.trace_mul_cycle, â† matrix.mul_assoc],
  nth_rewrite 0 â† conj_transpose_conj_transpose x,
  nth_rewrite 2 â† conj_transpose_conj_transpose x,
  nth_rewrite 4 â† conj_transpose_conj_transpose x,
  rw [â† matrix.conj_transpose_mul],
  simp_rw [matrix.mul_assoc, â† is_R_or_C.conj_eq_iff_re,
    star_ring_end_apply, trace_star],
  rw [conj_transpose_mul, conj_transpose_conj_transpose],
  refine âŸ¨rfl, trace_conj_transpose_mul_self_re_nonneg _âŸ©,
end

/-- given a positive definite matrix $Q$, we have
  $0\leq\Re(\textnormal{Tr}(Qx^*x))$ for any matrix $x$ -/
lemma nontracial.trace_conj_transpose_mul_self_re_nonneg [decidable_eq n]
  {Q : matrix n n ğ•œ} (x : matrix n n ğ•œ) (hQ : Q.pos_def) :
  0 â‰¤ is_R_or_C.re (Q â¬ xá´´ â¬ x).trace :=
(hQ.pos_semidef.trace_conj_transpose_mul_self_nonneg _).2

open_locale big_operators

@[simp] lemma finset.sum_abs_eq_zero_iff' {s : Type*} [fintype s] {x : s â†’ ğ•œ} :
  âˆ‘ (i : s) in finset.univ, â€–x iâ€– ^ 2 = 0
    â†” âˆ€ (i : s), â€–x iâ€– ^ 2 = 0 :=
begin
  have : âˆ€ i : s, 0 â‰¤ â€–x iâ€– ^ 2 := Î» i, sq_nonneg _,
  split,
  { intros h i,
    have h' : âˆ€ (i : s), i âˆˆ _  â†’ 0 â‰¤ â€–(x i)â€– ^ 2,
    { intros i hi, exact this _, },
    have h'' : âˆ‘ (i : s) in _, â€–(x i: ğ•œ)â€– ^ 2 = 0 := h,
    rw [finset.sum_eq_zero_iff_of_nonneg h'] at h'',
    simp only [finset.mem_univ, is_R_or_C.norm_sq_eq_zero, forall_true_left] at h'',
    exact h'' i, },
  { intros h,
    simp_rw [h, finset.sum_const_zero], },
end

lemma trace_conj_transpose_mul_self_eq_zero (x : matrix n n ğ•œ) :
  (xá´´ â¬ x).trace = 0 â†” x = 0 :=
begin
  split,
  { simp_rw [â† matrix.ext_iff, dmatrix.zero_apply, matrix.trace, matrix.diag,
      matrix.mul_apply, matrix.conj_transpose_apply, is_R_or_C.star_def,
      is_R_or_C.conj_mul, is_R_or_C.norm_sq_eq_def', is_R_or_C.of_real_pow],
    norm_cast,
    rw finset.sum_comm,
    simp_rw [â† finset.sum_product', finset.univ_product_univ, finset.sum_abs_eq_zero_iff',
      pow_eq_zero_iff two_pos, norm_eq_zero],
    intros h i j,
    exact h (i,j), },
  { intros h,
    simp_rw [h, matrix.mul_zero, matrix.trace_zero], },
end

/-- given a positive definite matrix $Q$, we get
  $\textnormal{Tr}(Qx^*x)=0$ if and only if $x=0$ for any matrix $x$ -/
lemma nontracial.trace_conj_transpose_mul_self_eq_zero [decidable_eq n]
  {x Q : matrix n n ğ•œ} (hQ : Q.pos_def) :
  (Q â¬ xá´´ â¬ x).trace = 0 â†” x = 0 :=
begin
  rcases (pos_semidef_iff Q).mp hQ.pos_semidef with âŸ¨y, rflâŸ©,
  rw [trace_mul_cycle, â† matrix.mul_assoc],
  nth_rewrite 0 â† conj_transpose_conj_transpose x,
  rw [â† conj_transpose_mul],
  simp_rw [matrix.mul_assoc],
  rw trace_conj_transpose_mul_self_eq_zero _,
  refine âŸ¨Î» h, _, Î» h, by rw [h, conj_transpose_zero, matrix.mul_zero]âŸ©,
  rw [â† star_eq_zero, mul_vec_eq],
  intros u,
  apply_fun (yá´´ â¬ y).to_lin',
  simp_rw [â† to_lin'_apply, â† linear_map.comp_apply, â† to_lin'_mul,
    â† mul_eq_mul, mul_assoc, mul_eq_mul, star_eq_conj_transpose],
  rw [h],
  simp_rw [matrix.mul_zero],
  refine function.bijective.injective _,
  rw [matrix.pos_semidef.invertible_iff_pos_def hQ.pos_semidef],
  exact hQ,
end

lemma is_hermitian.trace_conj_symm_star_mul {Q : matrix n n ğ•œ} (hQ : Q.is_hermitian)
  (x y : matrix n n ğ•œ) :
  (star_ring_end ğ•œ) (Q â¬ yá´´ â¬ x).trace = (Q â¬ xá´´ â¬ y).trace :=
begin
  simp_rw [star_ring_end_apply, â† trace_conj_transpose, conj_transpose_mul,
    conj_transpose_conj_transpose, hQ.eq, matrix.mul_assoc],
  rw [trace_mul_cycle'],
end

lemma conj_transpose_mul_self_eq_zero (x : matrix n n ğ•œ) :
  xá´´ â¬ x = 0 â†” x = 0 :=
begin
  refine âŸ¨_, Î» h, by rw [h, matrix.mul_zero]âŸ©,
  simp_rw [â† matrix.ext_iff, mul_apply, conj_transpose_apply, pi.zero_apply],
  intros h i j,
  specialize h j j,
  simp_rw [is_R_or_C.star_def, is_R_or_C.conj_mul, is_R_or_C.norm_sq_eq_def',
    â† is_R_or_C.of_real_sum, â† @is_R_or_C.of_real_zero ğ•œ, is_R_or_C.of_real_inj,
      finset.sum_abs_eq_zero_iff', sq_eq_zero_iff, norm_eq_zero] at h,
  exact h i,
end

lemma pos_semidef_smul_iff {x : matrix n n ğ•œ} (hx : x.pos_semidef) (Î± : nnreal) :
  (((Î± : â„) : ğ•œ) â€¢ x).pos_semidef :=
begin
  split,
  { calc (((Î± : â„) : ğ•œ) â€¢ x)á´´ = star ((Î± : â„) : ğ•œ) â€¢ xá´´ : by rw conj_transpose_smul
      ... = ((Î± : â„) : ğ•œ) â€¢ x : by rw [is_R_or_C.star_def, is_R_or_C.conj_of_real, hx.1.eq], },
  intros x,
  simp_rw [smul_mul_vec_assoc, dot_product_smul, smul_eq_mul, is_R_or_C.of_real_mul_re,
    mul_nonneg (nnreal.coe_nonneg _) (hx.2 _)],
end

end matrix

open matrix
open_locale matrix
lemma rank_one.euclidean_space.to_matrix' {ğ•œ : Type*} [is_R_or_C ğ•œ] {n : Type*}
  [fintype n] [decidable_eq n] (x y : euclidean_space ğ•œ n) :
  ((@rank_one ğ•œ (euclidean_space ğ•œ n) _ _ _ x y)
    : euclidean_space ğ•œ n â†’â‚—[ğ•œ] euclidean_space ğ•œ n).to_matrix'
      = col (x : n â†’ ğ•œ) â¬ (col (y : n â†’ ğ•œ))á´´ :=
begin
  simp_rw [â† matrix.ext_iff, linear_map.to_matrix'_apply, continuous_linear_map.coe_coe,
    rank_one_apply, pi.smul_apply, euclidean_space.inner_eq, dot_product, mul_boole,
    finset.sum_ite_eq', finset.mem_univ, if_true, mul_apply, conj_transpose_apply,
    col_apply, smul_eq_mul, fintype.univ_punit, finset.sum_const, finset.card_singleton,
    nsmul_eq_mul, algebra_map.coe_one, one_mul, mul_comm, pi.star_apply, eq_self_iff_true,
    forall_2_true_iff],
end

namespace matrix

lemma pos_semidef.col_mul_conj_transpose_col {n ğ•œ : Type*} [is_R_or_C ğ•œ] [fintype n]
  [decidable_eq n] [decidable_eq ğ•œ] (x : n â†’ ğ•œ) :
  (col x â¬ (col x)á´´ : matrix n n ğ•œ).pos_semidef :=
begin
  simp_rw [pos_semidef_iff_col_mul_conj_transpose_col, conj_transpose_col],
  by_cases nonempty n,
  { obtain i := h.some,
    let v : n â†’ (n â†’ ğ•œ) := Î» j, ite (j = i) x 0,
    use v,
    simp_rw [v, â† ext_iff, sum_apply, mul_apply, col_apply, row_apply,
      pi.star_apply, fintype.univ_punit, finset.sum_const, finset.card_singleton, one_smul,
      ite_apply, pi.zero_apply, star_ite, star_zero, mul_ite, mul_zero, ite_mul, zero_mul,
      finset.sum_ite_eq', finset.mem_univ, if_true, eq_self_iff_true, ite_eq_left_iff,
      not_true, false_implies_iff, forall_2_true_iff], },
  { simp_rw [not_nonempty_iff] at h,
    have : x = 0,
    { ext1 i,
      exfalso,
      exact (is_empty_iff.mp h) i, },
    use 0,
    simp only [this, â† ext_iff, sum_apply, mul_apply, pi.zero_apply, star_zero, row_apply,
      col_apply, mul_zero, finset.sum_const_zero, eq_self_iff_true, forall_2_true_iff], },
end

lemma pos_semidef.mul_conj_transpose_self {ğ•œ nâ‚ nâ‚‚ : Type*} [is_R_or_C ğ•œ]
  [fintype nâ‚] [fintype nâ‚‚] (x : matrix nâ‚ nâ‚‚ ğ•œ) :
  (x â¬ xá´´).pos_semidef  :=
begin
  refine âŸ¨is_hermitian_mul_conj_transpose_self _, _âŸ©,
  simp_rw [dot_product_eq_trace, â† conj_transpose_col, conj_transpose_mul, â† matrix.mul_assoc],
  intros y,
  rw [trace_mul_cycle, â† matrix.mul_assoc, â† conj_transpose_mul, matrix.mul_assoc],
  have : (((col y)á´´ â¬ x)á´´ â¬ ((col y)á´´ â¬ x)).trace = âˆ‘ (i : nâ‚‚) (j : _root_.unit), star (((col y)á´´ â¬ x) j i) * ((col y)á´´ â¬ x) j i,
  { simp_rw [â† conj_transpose_apply, â† mul_apply, trace_iff], },
  simp_rw [this, map_sum],
  apply finset.sum_nonneg',
  intros,
  apply finset.sum_nonneg',
  intros,
  simp_rw [is_R_or_C.star_def, is_R_or_C.conj_mul, is_R_or_C.of_real_re],
  exact is_R_or_C.norm_sq_nonneg _,
end

end matrix
