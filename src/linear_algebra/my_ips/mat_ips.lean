/-
Copyright (c) 2023 Monica Omar. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Monica Omar
-/
import linear_algebra.my_ips.functional
import linear_algebra.my_matrix.pos_def_rpow
import linear_algebra.mul''
import linear_algebra.my_ips.basic
import linear_algebra.pi_direct_sum
import linear_algebra.to_matrix_of_equiv

/-!

# The inner product space on finite dimensional C*-algebras

This file contains some basic results on the inner product space on finite dimensional C*-algebras.

-/

open_locale tensor_product functional

/-- A lemma that states the right multiplication property of a linear functional. -/
lemma linear_functional_right_mul {R A : Type*} [comm_semiring R] [semiring A]
  [algebra R A] [star_semigroup A] {Ï† : A â†’â‚—[R] R}
  (x y z : A) :
  Ï† (star (x * y) * z) = Ï† (star y * (star x * z)) :=
by rw [star_semigroup.star_mul, mul_assoc]

/-- A lemma that states the left multiplication property of a linear functional. -/
lemma linear_functional_left_mul {R A : Type*} [comm_semiring R] [semiring A]
  [algebra R A] [star_semigroup A] {Ï† : A â†’â‚—[R] R}
  (x y z : A) :
  Ï† (star x * (y * z)) = Ï† (star (star y * x) * z) :=
by rw [star_semigroup.star_mul, star_star, mul_assoc]

variables {k : Type*} [fintype k] [decidable_eq k] {s : k â†’ Type*} [Î  i, fintype (s i)]
  [Î  i, decidable_eq (s i)] {Ïˆ : Î  i, module.dual â„‚ (matrix (s i) (s i) â„‚)}
  [hÏˆ : Î  i, fact (Ïˆ i).is_faithful_pos_map]

open matrix

open_locale matrix big_operators

/-- A function that returns the direct sum of matrices for each index of type 'i'. -/
def module.dual.pi.matrix_block (Ïˆ : Î  i, module.dual â„‚ (matrix (s i) (s i) â„‚)) :
  Î  i, matrix (s i) (s i) â„‚ :=
âˆ‘ i, (Ïˆ i).matrix.include_block

/-- A function that returns a direct sum matrix. -/
def module.dual.pi.matrix (Ïˆ : Î  i, matrix (s i) (s i) â„‚ â†’â‚—[â„‚] â„‚) :
  matrix (Î£ i, s i) (Î£ i, s i) â„‚ :=
block_diagonal' (module.dual.pi.matrix_block Ïˆ)

/-- A lemma that states the inner product of two direct sum matrices is the sum of the inner products of their components. -/
lemma inner_pi_eq_sum [hÏˆ : Î  i, fact (Ïˆ i).is_faithful_pos_map]
  (x y : Î  i, matrix (s i) (s i) â„‚) :
  âŸªx, yâŸ«_â„‚ = âˆ‘ i, âŸªx i, y iâŸ«_â„‚ :=
rfl

lemma module.dual.pi.matrix_block_apply {i : k} :
  module.dual.pi.matrix_block Ïˆ i = (Ïˆ i).matrix :=
begin
  simp only [module.dual.pi.matrix_block, finset.sum_apply, include_block_apply, finset.sum_dite_eq', finset.mem_univ, if_true],
  refl,
end

/-- A function that returns a star algebra equivalence for each index of type 'i'. -/
def star_alg_equiv.pi {ğ•œ : Type*} [is_R_or_C ğ•œ]
  {k : Type u_1}  [fintype k]  [decidable_eq k] {s : k â†’ Type*}
  [Î  (i : k), fintype (s i)] [Î  (i : k), decidable_eq (s i)]
  (f : Î  i, matrix (s i) (s i) ğ•œ â‰ƒâ‹†â‚[ğ•œ] matrix (s i) (s i) ğ•œ) :
  (Î  i, matrix (s i) (s i) ğ•œ) â‰ƒâ‹†â‚[ğ•œ] Î  i, matrix (s i) (s i) ğ•œ :=
{ to_fun := Î» x i, f i (x i),
  inv_fun := Î» x i, (f i).symm (x i),
  left_inv := Î» a, by {
    simp only [star_alg_equiv.symm_apply_apply], },
  right_inv := Î» a, by {
    simp only [star_alg_equiv.apply_symm_apply], },
  map_add' := Î» a y, by {
    simp only [pi.add_apply, map_add],
    refl, },
  map_smul' := Î» r a, by {
    simp only [pi.smul_apply, smul_hom_class.map_smul],
    refl, },
  map_mul' := Î» a b, by {
    simp only [pi.mul_apply, _root_.map_mul],
    refl, },
  map_star' := Î» a, by {
    simp only [pi.star_apply, map_star],
    refl, } }

lemma star_alg_equiv.pi_apply {ğ•œ : Type*} [is_R_or_C ğ•œ]
  {k : Type u_1}  [fintype k]  [decidable_eq k] {s : k â†’ Type*}
  [Î  (i : k), fintype (s i)] [Î  (i : k), decidable_eq (s i)]
  (f : Î  i, matrix (s i) (s i) ğ•œ â‰ƒâ‹†â‚[ğ•œ] matrix (s i) (s i) ğ•œ)
  (x : Î  i, matrix (s i) (s i) ğ•œ) (i : k) :
  star_alg_equiv.pi f x i = f i (x i) :=
rfl

/-- the unitary element from the star algebraic equivalence -/
noncomputable def star_alg_equiv.pi.unitary {ğ•œ : Type*} [is_R_or_C ğ•œ]
  {k : Type u_1}  [fintype k]  [decidable_eq k] {s : k â†’ Type*}
  [Î  (i : k), fintype (s i)] [Î  (i : k), decidable_eq (s i)]
  (f : Î  i, matrix (s i) (s i) ğ•œ â‰ƒâ‹†â‚[ğ•œ] matrix (s i) (s i) ğ•œ) :
  Î  i, unitary_group (s i) ğ•œ :=
Î» i, (f i).unitary

lemma star_alg_equiv.pi.unitary_apply {ğ•œ : Type*} [is_R_or_C ğ•œ]
  {k : Type u_1}  [fintype k]  [decidable_eq k] {s : k â†’ Type*}
  [Î  (i : k), fintype (s i)] [Î  (i : k), decidable_eq (s i)]
  (f : Î  i, matrix (s i) (s i) ğ•œ â‰ƒâ‹†â‚[ğ•œ] matrix (s i) (s i) ğ•œ) (a : k) :
  (star_alg_equiv.pi.unitary f) a = (f a).unitary :=
rfl

/-- any $^*$-isomorphism on $\bigoplus_i M_{n_i}$ is an inner automorphism -/
lemma star_alg_equiv.of_pi_is_inner {ğ•œ : Type*} [is_R_or_C ğ•œ]
  {k : Type u_1}  [fintype k]  [decidable_eq k] {s : k â†’ Type*}
  [Î  (i : k), fintype (s i)] [Î  (i : k), decidable_eq (s i)]
  (f : Î  i, matrix (s i) (s i) ğ•œ â‰ƒâ‹†â‚[ğ•œ] matrix (s i) (s i) ğ•œ) :
  unitary.inner_aut_star_alg ğ•œ
    (unitary.pi (star_alg_equiv.pi.unitary f))
    = star_alg_equiv.pi f :=
begin
  simp_rw [star_alg_equiv.ext_iff, unitary.inner_aut_star_alg_apply, function.funext_iff,
    pi.mul_apply, unitary.pi_apply, unitary.coe_star, pi.star_apply, unitary.pi_apply,
    star_alg_equiv.pi_apply, star_alg_equiv.pi.unitary_apply],
  intros,
  rw [â† unitary.coe_star, â† @unitary.inner_aut_star_alg_apply ğ•œ _ _ _ _ _
    ((f a_1).unitary) (a a_1)],
  congr,
  exact star_alg_equiv.eq_inner_aut _,
end


def incl_pi {i : k} (x : s i â†’ â„‚) :
  (Î£ j, s j) â†’ â„‚ :=
Î» j, dite (i = j.1) (Î» h, x (by { rw h, exact j.2, })) (Î» h, 0)
def excl_pi (x : (Î£ j, s j) â†’ â„‚) (i : k) :
  s i â†’ â„‚ :=
Î» j, x âŸ¨i,jâŸ©

private lemma pi.forall_left_mul (x y : Î  i, matrix (s i) (s i) â„‚) :
  (âˆ€ a, a * x = a * y) â†” x = y :=
begin
  split,
  { intros h,
    specialize h 1,
    simp_rw one_mul at h,
    exact h, },
  { rintros rfl a,
    refl, },
end

lemma module.dual.pi.apply'' (Ïˆ : Î  i, matrix (s i) (s i) â„‚ â†’â‚—[â„‚] â„‚)
  (x : Î  i, matrix (s i) (s i) â„‚) :
  module.dual.pi Ïˆ x
    = (block_diagonal' (module.dual.pi.matrix_block Ïˆ) * block_diagonal' x).trace :=
begin
  simp_rw [module.dual.pi.apply', module.dual.pi.matrix_block,
    â† block_diagonal'_alg_hom_apply, map_sum, finset.sum_mul, trace_sum, mul_eq_mul],
end

lemma star_alg_equiv.pi_is_trace_preserving
  (f : Î  i, matrix (s i) (s i) â„‚ â‰ƒâ‹†â‚[â„‚] matrix (s i) (s i) â„‚) (x : Î  i, matrix (s i) (s i) â„‚) :
  (block_diagonal'_alg_hom ((star_alg_equiv.pi f) x)).trace
    = (block_diagonal'_alg_hom x).trace :=
begin
  rw matrix_eq_sum_include_block ((star_alg_equiv.pi f) x),
  nth_rewrite_rhs 0 matrix_eq_sum_include_block x,
  simp only [map_sum, trace_sum],
  simp only [block_diagonal'_alg_hom_apply, block_diagonal'_include_block_trace,
    star_alg_equiv.pi_apply, star_alg_equiv.trace_preserving],
end

lemma star_alg_equiv.pi_symm_apply_apply
  (f : Î  i, matrix (s i) (s i) â„‚ â‰ƒâ‹†â‚[â„‚] matrix (s i) (s i) â„‚)
  (x : Î  i, matrix (s i) (s i) â„‚) :
  (star_alg_equiv.pi (Î» i, (f i).symm))
    ((star_alg_equiv.pi f) x) = x :=
begin
  ext1,
  simp only [star_alg_equiv.pi_apply, star_alg_equiv.symm_apply_apply],
end

lemma module.dual.pi.apply_eq_of (Ïˆ : Î  i, module.dual â„‚ (matrix (s i) (s i) â„‚))
  (x : Î  i, matrix (s i) (s i) â„‚)
  (h : âˆ€ a, module.dual.pi Ïˆ a = (block_diagonal' x * block_diagonal' a).trace) :
  x = module.dual.pi.matrix_block Ïˆ :=
begin
  ext1,
  simp only [module.dual.pi.matrix_block_apply],
  apply module.dual.apply_eq_of,
  intros a,
  let a' := include_block a,
  have ha' : a = a' x_1 := by simp only [a', include_block_apply_same],
  specialize h a',
  simp_rw [ha', â† module.dual.pi.apply_single_block,
    â† mul_eq_mul, â† pi.mul_apply, â† block_diagonal'_include_block_trace,
    â† ha', pi.mul_apply, â† ha'],
  simp only [â† block_diagonal'_alg_hom_apply, â† _root_.map_mul, a',
    mul_include_block] at h,
  exact h,
end

lemma star_alg_equiv.pi_symm_apply_eq
  (f : Î  i, matrix (s i) (s i) â„‚ â‰ƒâ‹†â‚[â„‚] matrix (s i) (s i) â„‚)
  (x y : Î  i, matrix (s i) (s i) â„‚) :
  star_alg_equiv.pi (Î» i, (f i).symm) x = y
    â†” x = star_alg_equiv.pi f y :=
begin
  split; rintros rfl; ext1; simp only [star_alg_equiv.pi_apply],
  { rw star_alg_equiv.apply_symm_apply, },
  { rw star_alg_equiv.symm_apply_apply, },
end

lemma unitary.inj_mul {A : Type*} [monoid A]
  [star_semigroup A] (U : unitary A) (x y : A) :
  x = y â†” x * U = y * U :=
begin
  rw [is_unit.mul_left_inj],
  { rw [â† unitary.coe_to_units_apply],
    exact (unitary.to_units U).is_unit, },
end

section single_block
/-!
  ## Section `single_block`
-/

variables {n : Type*} [decidable_eq n] [fintype n]
  {Ï† : module.dual â„‚ (matrix n n â„‚)} [hÏ† : fact Ï†.is_faithful_pos_map]

namespace module.dual.is_faithful_pos_map

lemma inner_eq [hÏ† : fact Ï†.is_faithful_pos_map]
  (x y : matrix n n â„‚) :
  âŸªx, yâŸ«_â„‚  = Ï† (xá´´ â¬ y) :=
rfl

lemma inner_eq' [hÏ† : fact Ï†.is_faithful_pos_map]
  (x y : matrix n n â„‚) :
  âŸªx, yâŸ«_â„‚ = (Ï†.matrix â¬ xá´´ â¬ y).trace :=
by rw [inner_eq, Ï†.apply, matrix.mul_assoc]

def matrix_is_pos_def (hÏ† : Ï†.is_faithful_pos_map) :
  Ï†.matrix.pos_def :=
Ï†.is_faithful_pos_map_iff_of_matrix.mp hÏ†

lemma mul_right (hÏ† : Ï†.is_faithful_pos_map)
  (x y z : matrix n n â„‚) :
  Ï† (xá´´ â¬ (y â¬ z)) = Ï† ((x â¬ (Ï†.matrix â¬ zá´´ â¬ Ï†.matrixâ»Â¹))á´´ â¬ y) :=
begin
  haveI := hÏ†.matrix_is_pos_def.invertible,
  simp_rw [Ï†.apply, matrix.conj_transpose_mul,
    matrix.conj_transpose_conj_transpose, hÏ†.matrix_is_pos_def.1.eq, hÏ†.matrix_is_pos_def.inv.1.eq,
    â† matrix.mul_assoc, matrix.mul_assoc, matrix.mul_inv_cancel_left_of_invertible],
  rw [matrix.trace_mul_cycle', matrix.mul_assoc, â† matrix.trace_mul_cycle', matrix.mul_assoc],
end

lemma inner_left_mul [hÏ† : fact Ï†.is_faithful_pos_map]
  (x y z : matrix n n â„‚) :
  âŸªx â¬ y, zâŸ«_â„‚ = âŸªy, xá´´ â¬ zâŸ«_â„‚ :=
linear_functional_right_mul _ _ _

lemma inner_right_mul [hÏ† : fact Ï†.is_faithful_pos_map]
  (x y z : matrix n n â„‚) :
  âŸªx, y â¬ zâŸ«_â„‚ = âŸªyá´´ â¬ x, zâŸ«_â„‚ :=
linear_functional_left_mul _ _ _

lemma inner_left_conj [hÏ† : fact Ï†.is_faithful_pos_map]
  (x y z : matrix n n â„‚) :
  âŸªx, y â¬ zâŸ«_â„‚ = âŸªx â¬ (Ï†.matrix â¬ zá´´ â¬ Ï†.matrixâ»Â¹), yâŸ«_â„‚ :=
hÏ†.elim.mul_right _ _ _

lemma mul_left (hÏ† : Ï†.is_faithful_pos_map) (x y z : matrix n n â„‚) :
  Ï† ((x â¬ y)á´´ â¬ z) = Ï† (xá´´ â¬ (z â¬ (Ï†.matrix â¬ yá´´ â¬ Ï†.matrixâ»Â¹))) :=
begin
  letI := fact.mk hÏ†,
  rw [â† inner_eq, â† inner_product_space.core.inner_conj_symm, inner_left_conj,
    inner_product_space.core.inner_conj_symm],
  refl,
end

lemma inner_right_conj [hÏ† : fact Ï†.is_faithful_pos_map] (x y z : matrix n n â„‚) :
  âŸªx â¬ y, zâŸ«_â„‚ = âŸªx, z â¬ (Ï†.matrix â¬ yá´´ â¬ Ï†.matrixâ»Â¹)âŸ«_â„‚ :=
hÏ†.elim.mul_left _ _ _

lemma adjoint_eq [hÏ† : fact Ï†.is_faithful_pos_map] :
  Ï†.adjoint = (algebra.linear_map â„‚ (matrix n n â„‚) : â„‚ â†’â‚—[â„‚] matrix n n â„‚) :=
begin
  rw linear_map.ext_iff,
  intros x,
  apply @ext_inner_right â„‚,
  intros y,
  rw [linear_map.adjoint_inner_left, algebra.linear_map_apply,
    algebra.algebra_map_eq_smul_one, inner_product_space.core.inner_smul_left,
    inner_eq, conj_transpose_one, matrix.one_mul],
  refl,
end


/-- The adjoint of a star-algebraic equivalence $f$ on matrix algebras is given by
  $$f^*\colon x \mapsto f^{-1}(x Q) Q^{-1},$$
  where $Q$ is `hÏ†.matrix`. -/
lemma star_alg_equiv_adjoint_eq [hÏ† : fact Ï†.is_faithful_pos_map]
  (f : matrix n n â„‚ â‰ƒâ‹†â‚[â„‚] matrix n n â„‚) (x : matrix n n â„‚) :
  ((f : matrix n n â„‚ â‰ƒâ‹†â‚[â„‚] matrix n n â„‚).to_alg_equiv.to_linear_map : matrix n n â„‚ â†’â‚—[â„‚] matrix n n â„‚).adjoint x
    = (f.symm (x â¬ Ï†.matrix) : matrix n n â„‚) â¬ Ï†.matrixâ»Â¹ :=
begin
  haveI := hÏ†.elim.matrix_is_pos_def.invertible,
  apply @ext_inner_left â„‚,
  intros a,
  simp_rw [linear_map.adjoint_inner_right, alg_equiv.to_linear_map_apply,
    star_alg_equiv.coe_to_alg_equiv],
  obtain âŸ¨U, rflâŸ© := f.of_matrix_is_inner,
  simp_rw [inner_aut_star_alg_apply, inner_aut_star_alg_symm_apply, matrix.mul_assoc],
  nth_rewrite_rhs 0 â† matrix.mul_assoc (Ï†.matrix),
  nth_rewrite_rhs 0 â† matrix.mul_assoc,
  rw [inner_left_conj, inner_right_mul],
  simp_rw [conj_transpose_mul, hÏ†.elim.matrix_is_pos_def.1.eq, hÏ†.elim.matrix_is_pos_def.inv.1.eq,
    â† star_eq_conj_transpose, â† unitary_group.star_coe_eq_coe_star, star_star,
    matrix.mul_inv_cancel_left_of_invertible, matrix.mul_assoc,
    mul_inv_of_invertible, matrix.mul_one],
end

lemma star_alg_equiv_unitary_commute_iff [hÏ† : fact Ï†.is_faithful_pos_map]
  (f : matrix n n â„‚ â‰ƒâ‹†â‚[â„‚] matrix n n â„‚) :
  commute Ï†.matrix f.unitary â†” f Ï†.matrix = Ï†.matrix :=
begin
  rw [commute, semiconj_by],
  nth_rewrite 2 [â† star_alg_equiv.eq_inner_aut f],
  rw [inner_aut_star_alg_apply, â† unitary_group.star_coe_eq_coe_star],
  nth_rewrite 1 [unitary_group.injective_mul f.unitary],
  simp_rw [matrix.mul_assoc, unitary_group.star_mul_self, matrix.mul_one, mul_eq_mul, eq_comm],
end

/-- Let `f` be a  star-algebraic equivalence on matrix algebras. Then tfae:

* `f Ï†.matrix = Ï†.matrix`,
* `f.adjoint = fâ»Â¹`,
* `Ï† âˆ˜ f = Ï†`,
* `âˆ€ x y, âŸªf x, f yâŸ«_â„‚ = âŸªx, yâŸ«_â„‚`,
* `âˆ€ x, â€–f xâ€– = â€–xâ€–`,
* `Ï†.matrix` commutes with `f.unitary`.
-/
lemma star_alg_equiv_is_isometry_tfae [hÏ† : fact Ï†.is_faithful_pos_map]
  [nontrivial n] (f : matrix n n â„‚ â‰ƒâ‹†â‚[â„‚] matrix n n â„‚) :
  tfae [f Ï†.matrix = Ï†.matrix,
    ((f : matrix n n â„‚ â‰ƒâ‹†â‚[â„‚] matrix n n â„‚).to_alg_equiv.to_linear_map : matrix n n â„‚ â†’â‚—[â„‚] matrix n n â„‚).adjoint = f.symm.to_alg_equiv.to_linear_map,
    Ï† âˆ˜â‚— f.to_alg_equiv.to_linear_map = Ï†,
    âˆ€ x y, âŸªf x, f yâŸ«_â„‚ = âŸªx, yâŸ«_â„‚,
    âˆ€ x : matrix n n â„‚, â€–f xâ€– = â€–xâ€–,
    commute Ï†.matrix f.unitary] :=
begin
  tfae_have : 5 â†” 2,
  { simp_rw [inner_product_space.core.norm_eq_sqrt_inner,
    real.sqrt_inj inner_product_space.core.inner_self_nonneg
      inner_product_space.core.inner_self_nonneg, â† complex.of_real_inj,
    inner_self_re, â† @sub_eq_zero _ _ _ âŸª_, _âŸ«_â„‚],
    have : âˆ€ x y, âŸªf x, f yâŸ«_â„‚ - âŸªx, yâŸ«_â„‚
      = âŸª(f.to_alg_equiv.to_linear_map.adjoint âˆ˜â‚— f.to_alg_equiv.to_linear_map - 1) x, yâŸ«_â„‚,
    { intros x y,
      simp only [linear_map.sub_apply, linear_map.one_apply, inner_sub_left,
        linear_map.comp_apply, linear_map.adjoint_inner_left, star_alg_equiv.coe_to_alg_equiv,
        alg_equiv.to_linear_map_apply], },
    simp_rw [this, inner_map_self_eq_zero, sub_eq_zero, star_alg_equiv.comp_eq_iff,
      linear_map.one_comp], },
  rw tfae_5_iff_2,
  tfae_have : 4 â†” 3,
  { simp_rw [inner_eq, â† star_eq_conj_transpose, â† map_star f, â† mul_eq_mul,
      â† _root_.map_mul f, linear_map.ext_iff, linear_map.comp_apply, alg_equiv.to_linear_map_apply,
      star_alg_equiv.coe_to_alg_equiv],
    refine âŸ¨Î» h x, _, Î» h x y, h _âŸ©,
    rw [â† matrix.one_mul x, â† star_one, â† mul_eq_mul],
    exact h _ _, },
  rw tfae_4_iff_3,
  haveI := hÏ†.elim.matrix_is_pos_def.invertible,
  simp_rw [linear_map.ext_iff, star_alg_equiv_adjoint_eq f, linear_map.comp_apply,
    alg_equiv.to_linear_map_apply, star_alg_equiv.coe_to_alg_equiv,
    mul_inv_eq_iff_eq_mul_of_invertible, Ï†.apply,
    star_alg_equiv.symm_apply_eq, â† mul_eq_mul, _root_.map_mul, star_alg_equiv.apply_symm_apply,
    â† forall_left_mul Ï†.matrix, @eq_comm _ Ï†.matrix],
  tfae_have : 1 â†” 2,
  { rw iff_self, trivial, },
  tfae_have : 1 â†’ 3,
  { intros i x,
    nth_rewrite 0 â† i,
    rw [â† _root_.map_mul, f.trace_preserving], },
  tfae_have : 3 â†’ 1,
  { intros i,
    simp_rw [â† f.symm.trace_preserving (Ï†.matrix * (f _)), _root_.map_mul,
      star_alg_equiv.symm_apply_apply, mul_eq_mul, â† Ï†.apply,
      @eq_comm _ _ (Ï† _)] at i,
    obtain âŸ¨Q, hQ, hâŸ© := module.dual.eq_trace_unique Ï†,
    have := h _ i,
    rw star_alg_equiv.symm_apply_eq at this,
    nth_rewrite_rhs 0 this,
    congr',
    exact h _ Ï†.apply, },
  rw [star_alg_equiv_unitary_commute_iff],
  tfae_finish,
end

protected noncomputable def basis (hÏ† : Ï†.is_faithful_pos_map) :
  basis (n Ã— n) â„‚ (matrix n n â„‚) :=
begin
  let hQ := hÏ†.matrix_is_pos_def,
  refine basis.mk _ _,
  { exact Î» ij, std_basis_matrix ij.1 ij.2 1 â¬ hÏ†.matrix_is_pos_def.rpow (-(1/2)), },
  { have := (std_basis â„‚ n n).linear_independent,
    simp_rw [linear_independent, linear_map.ker_eq_bot, injective_iff_map_eq_zero,
      finsupp.total_apply, finsupp.sum] at this âŠ¢,
    simp_rw [â† mul_eq_mul, â† smul_mul_assoc, â† finset.sum_mul],
    by_cases is_empty n,
    { haveI := h,
      simp only [eq_iff_true_of_subsingleton, forall_const], },
    rw not_is_empty_iff at h,
    have t1 : âˆ€ (a : n Ã— n â†’â‚€ â„‚), (âˆ‘ (x : n Ã— n) in a.support,
      (a x) â€¢ (std_basis_matrix x.fst x.snd 1 : matrix n n â„‚))
        * hQ.rpow (-(1 / 2)) = 0
      â†” (âˆ‘ (x : n Ã— n) in a.support, a x â€¢ (std_basis_matrix x.fst x.snd 1 : matrix n n â„‚))
        * hQ.rpow (-(1 / 2)) * hQ.rpow (1 / 2)
      = 0 * hQ.rpow (1 / 2),
    { intros a,
      split; intros h,
      { rw h, },
      { simp_rw [mul_assoc, mul_eq_mul, matrix.pos_def.rpow_mul_rpow, neg_add_self,
          matrix.pos_def.rpow_zero, matrix.mul_one] at h,
        rw [h, matrix.zero_mul, zero_mul], }, },
    simp_rw [t1, mul_assoc, mul_eq_mul, matrix.pos_def.rpow_mul_rpow,
      neg_add_self, matrix.pos_def.rpow_zero, matrix.zero_mul, matrix.mul_one,
      â† std_basis_eq_std_basis_matrix â„‚, prod.mk.eta],
    exact this, },
  { simp_rw [top_le_iff],
    ext,
    simp_rw [submodule.mem_top, iff_true, mem_span_range_iff_exists_fun,
      â† smul_mul, â† mul_eq_mul, â† finset.sum_mul, â† matrix.ext_iff, mul_eq_mul,
      mul_apply, matrix.sum_apply, pi.smul_apply, std_basis_matrix, smul_ite, smul_zero,
      â† prod.mk.inj_iff, prod.mk.eta, finset.sum_ite_eq', finset.mem_univ, if_true,
      smul_mul_assoc, one_mul],
    existsi (Î» ij : n Ã— n, ((x â¬ hQ.rpow (1/2)) : matrix n n â„‚) ij.1 ij.2),
    simp_rw [smul_eq_mul, â† mul_apply, matrix.mul_assoc, matrix.pos_def.rpow_mul_rpow,
      add_neg_self, matrix.pos_def.rpow_zero, matrix.mul_one, eq_self_iff_true,
      forall_2_true_iff], },
end

protected lemma basis_apply (hÏ† : Ï†.is_faithful_pos_map) (ij : n Ã— n) :
  hÏ†.basis ij = std_basis_matrix ij.1 ij.2 (1 : â„‚) â¬ hÏ†.matrix_is_pos_def.rpow (-(1/2 : â„)) :=
begin
  rw [is_faithful_pos_map.basis, basis.mk_apply],
end


local notation `|` x `âŸ©âŸ¨` y `|` := @rank_one â„‚ _ _ _ _ x y

protected noncomputable def to_matrix (hÏ† : Ï†.is_faithful_pos_map) :
  (matrix n n â„‚ â†’â‚—[â„‚] matrix n n â„‚) â‰ƒâ‚[â„‚] matrix (n Ã— n) (n Ã— n) â„‚ :=
linear_map.to_matrix_alg_equiv hÏ†.basis

lemma basis_is_orthonormal [hÏ† : fact Ï†.is_faithful_pos_map] :
  orthonormal â„‚ hÏ†.elim.basis :=
begin
  rw orthonormal_iff_ite,
  simp_rw [module.dual.is_faithful_pos_map.basis_apply],
  simp_rw [inner_eq', conj_transpose_mul, (pos_def.rpow.is_hermitian _ _).eq,
    std_basis_matrix.star_one, matrix.mul_assoc, â† matrix.mul_assoc _ (std_basis_matrix _ _ _),
    std_basis_matrix_mul, one_mul, matrix.smul_mul, matrix.mul_smul,
    trace_smul, smul_eq_mul, boole_mul],
  let Q := Ï†.matrix,
  let hQ := hÏ†.elim.matrix_is_pos_def,
  have : âˆ€ i j : n,
    (Q â¬ (hQ.rpow (-(1 / 2) : â„) â¬ (std_basis_matrix i j 1
      â¬ hQ.rpow (-(1 / 2) : â„)))).trace = ite (i = j) (1 : â„‚) (0 : â„‚) := Î» i j,
  calc (Q â¬ (hQ.rpow (-(1 / 2) : â„) â¬ (std_basis_matrix i j 1
      â¬ hQ.rpow (-(1 / 2) : â„)))).trace
    = ((hQ.rpow (-(1/2) : â„) â¬ hQ.rpow 1 â¬ hQ.rpow (-(1/2) : â„))
      â¬ std_basis_matrix i j 1).trace :
  by { simp_rw [pos_def.rpow_one_eq_self, matrix.mul_assoc],
    rw [â† trace_mul_cycle', trace_mul_comm],
    simp_rw [matrix.mul_assoc],
    rw trace_mul_comm,
    simp_rw [matrix.mul_assoc], }
    ... = ((hQ.rpow (-(1/2) + 1 + -(1/2) : â„)) â¬ std_basis_matrix i j 1).trace :
  by { simp_rw [pos_def.rpow_mul_rpow], }
    ... = (hQ.rpow 0 â¬ std_basis_matrix i j 1).trace : by norm_num
    ... = ite (i = j) 1 0 : by { simp_rw [pos_def.rpow_zero, matrix.one_mul,
      std_basis_matrix.trace], },
  simp_rw [this, â† ite_and, â† prod.eq_iff_fst_eq_snd_eq, eq_self_iff_true, forall_2_true_iff],
end

protected noncomputable def orthonormal_basis
  [hÏ† : fact Ï†.is_faithful_pos_map] :
  orthonormal_basis (n Ã— n) â„‚ (matrix n n â„‚) :=
hÏ†.elim.basis.to_orthonormal_basis basis_is_orthonormal

protected lemma orthonormal_basis_apply [hÏ† : fact Ï†.is_faithful_pos_map]
  (ij : n Ã— n) :
  (is_faithful_pos_map.orthonormal_basis : orthonormal_basis (n Ã— n) â„‚ (matrix n n â„‚)) ij
    = std_basis_matrix ij.1 ij.2 (1 : â„‚) â¬ hÏ†.elim.matrix_is_pos_def.rpow (-(1/2 : â„)) :=
begin
  rw [â† is_faithful_pos_map.basis_apply, is_faithful_pos_map.orthonormal_basis,
    basis.coe_to_orthonormal_basis],
end

lemma inner_coord [hÏ† : fact Ï†.is_faithful_pos_map]
  (ij : n Ã— n) (y : matrix n n â„‚) :
  âŸª(is_faithful_pos_map.orthonormal_basis : orthonormal_basis _ _ _) ij, yâŸ«_â„‚
    = (y â¬ hÏ†.elim.matrix_is_pos_def.rpow (1 / 2)) ij.1 ij.2 :=
begin
  let Q := Ï†.matrix,
  let hQ := hÏ†.elim.matrix_is_pos_def,
  simp_rw [inner_eq', is_faithful_pos_map.orthonormal_basis_apply, conj_transpose_mul,
    (matrix.pos_def.rpow.is_hermitian hQ _).eq, â† matrix.mul_assoc,
    std_basis_matrix_conj_transpose, star_one],
  have := calc Q â¬ hQ.rpow (-(1 / 2)) = hQ.rpow (1) â¬ hQ.rpow (-(1 / 2)) :
  by rw [matrix.pos_def.rpow_one_eq_self]
    ... = hQ.rpow (1 + -(1 / 2)) : by rw [matrix.pos_def.rpow_mul_rpow]
    ... = hQ.rpow (1 / 2) : by norm_num,
  rw this,
  simp_rw [trace_iff, mul_apply, std_basis_matrix, mul_boole, ite_and],
  simp only [finset.sum_ite_eq, finset.mem_univ, if_true, ite_mul, zero_mul],
  simp_rw [mul_comm],
end

protected lemma basis_repr_apply [hÏ† : fact Ï†.is_faithful_pos_map]
  (x : matrix n n â„‚) (ij : n Ã— n) :
  hÏ†.elim.basis.repr x ij = âŸªhÏ†.elim.basis ij, xâŸ«_â„‚ :=
begin
  rw [is_faithful_pos_map.basis_apply, â† is_faithful_pos_map.orthonormal_basis_apply,
    â† orthonormal_basis.repr_apply_apply],
  refl,
end

protected lemma to_matrix_symm_apply
  [hÏ† : fact Ï†.is_faithful_pos_map]
  (x : matrix (n Ã— n) (n Ã— n) â„‚) :
  hÏ†.elim.to_matrix.symm x
    = âˆ‘ (i j k l : n), (x (i,j) (k,l) : â„‚)
      â€¢ (|(hÏ†.elim.basis (i, j))âŸ©âŸ¨(hÏ†.elim.basis (k, l))|) :=
begin
  rw [is_faithful_pos_map.to_matrix, linear_map.to_matrix_alg_equiv_symm, linear_map.ext_iff],
  intros a,
  simp_rw [to_lin_alg_equiv_apply, mul_vec, dot_product, is_faithful_pos_map.basis_repr_apply,
    linear_map.sum_apply, linear_map.smul_apply, continuous_linear_map.coe_coe,
    rank_one_apply, is_faithful_pos_map.basis_apply, finset.sum_smul],
  repeat { nth_rewrite_rhs 0 â† finset.sum_product',
    rw [finset.univ_product_univ],
    apply finset.sum_congr rfl,
    intros ij hij, },
  simp_rw [smul_smul, prod.mk.eta],
end

end module.dual.is_faithful_pos_map

local notation `|` x `âŸ©âŸ¨` y `|` := @rank_one â„‚ _ _ _ _ x y
lemma module.dual.eq_rank_one_of_faithful_pos_map
  [hÏ† : fact Ï†.is_faithful_pos_map]
  (x : matrix n n â„‚ â†’â‚—[â„‚] matrix n n â„‚) :
  x = âˆ‘ i j k l : n, hÏ†.elim.to_matrix x (i,j) (k,l)
    â€¢ (|hÏ†.elim.basis (i, j)âŸ©âŸ¨hÏ†.elim.basis (k, l)|) :=
by rw [â† module.dual.is_faithful_pos_map.to_matrix_symm_apply, alg_equiv.symm_apply_apply]

end single_block

---------

section direct_sum

/-! # Section direct_sum -/

lemma linear_map.sum_single_comp_proj {R : Type*} {Î¹ : Type*} [fintype Î¹] [decidable_eq Î¹] [semiring R] {Ï† : Î¹ â†’ Type*}
  [Î  (i : Î¹), add_comm_monoid (Ï† i)] [Î  (i : Î¹), module R (Ï† i)] :
  âˆ‘ i : Î¹, linear_map.single i âˆ˜â‚— linear_map.proj i
    = (linear_map.id : (Î  i, Ï† i) â†’â‚—[R] (Î  i, Ï† i)) :=
begin
  simp_rw [linear_map.ext_iff, linear_map.sum_apply, linear_map.id_apply,
    linear_map.comp_apply, linear_map.proj_apply,
    linear_map.coe_single, pi.single, function.funext_iff,
    finset.sum_apply, function.update, pi.zero_apply,
    finset.sum_dite_eq, finset.mem_univ, if_true],
  intros x y, trivial,
end

lemma linear_map.lrsum_eq_single_proj_lrcomp (f : (Î  i, matrix (s i) (s i) â„‚) â†’â‚—[â„‚] (Î  i, matrix (s i) (s i) â„‚)) :
  âˆ‘ r p, (linear_map.single r) âˆ˜â‚— (linear_map.proj r) âˆ˜â‚— f
    âˆ˜â‚— (linear_map.single p) âˆ˜â‚— (linear_map.proj p) = f :=
calc âˆ‘ r p, (linear_map.single r) âˆ˜â‚— (linear_map.proj r) âˆ˜â‚— f
    âˆ˜â‚— (linear_map.single p) âˆ˜â‚— (linear_map.proj p)
  = (âˆ‘ r, (linear_map.single r) âˆ˜â‚— (linear_map.proj r)) âˆ˜â‚— f
      âˆ˜â‚— âˆ‘ p, (linear_map.single p) âˆ˜â‚— (linear_map.proj p) :
  by simp_rw [linear_map.sum_comp, linear_map.comp_sum, linear_map.comp_assoc]
  ... = linear_map.id âˆ˜â‚— f âˆ˜â‚— linear_map.id : by rw linear_map.sum_single_comp_proj
  ... = f : by rw [linear_map.id_comp, linear_map.comp_id]

namespace module.dual.pi.is_faithful_pos_map

lemma inner_eq [hÏˆ : Î  i, fact (Ïˆ i).is_faithful_pos_map]
  (x y : Î  i, matrix (s i) (s i) â„‚) :
  âŸªx, yâŸ«_â„‚ = module.dual.pi Ïˆ (star x * y) :=
rfl

lemma inner_eq' [hÏˆ : Î  i, fact (Ïˆ i).is_faithful_pos_map]
  (x y : Î  i, matrix (s i) (s i) â„‚) :
  âŸªx, yâŸ«_â„‚ = âˆ‘ i, ((Ïˆ i).matrix â¬ (x i)á´´ â¬ y i).trace :=
begin
  simp only [inner_eq, module.dual.pi.apply, pi.mul_apply,
    matrix.mul_eq_mul, matrix.star_eq_conj_transpose, pi.star_apply, matrix.mul_assoc],
end

lemma inner_left_mul [hÏˆ : Î  i, fact (Ïˆ i).is_faithful_pos_map]
  (x y z : Î  i, matrix (s i) (s i) â„‚) :
  âŸªx * y, zâŸ«_â„‚ = âŸªy, star x * zâŸ«_â„‚ :=
@linear_functional_right_mul _ _ _ _ _ _ (module.dual.pi Ïˆ) _ _ _

lemma mul_right (hÏˆ : Î  i, (Ïˆ i).is_faithful_pos_map)
  (x y z : Î  i, matrix (s i) (s i) â„‚) :
  module.dual.pi Ïˆ (star x * (y * z))
    = module.dual.pi Ïˆ
      (star (x *
        ((module.dual.pi.matrix_block Ïˆ) * (star z)
          * (module.dual.pi.matrix_block Ïˆ)â»Â¹)) * y) :=
begin
  letI := Î» i, fact.mk (hÏˆ i),
  rw [â† inner_eq],
  simp only [inner_eq'],
  simp_rw [â† module.dual.is_faithful_pos_map.inner_eq', pi.mul_apply, matrix.mul_eq_mul,
    module.dual.is_faithful_pos_map.inner_left_conj,
    â† inner_eq, inner_pi_eq_sum,
    pi.mul_apply, pi.inv_apply, pi.star_apply, matrix.mul_eq_mul,
    matrix.star_eq_conj_transpose, module.dual.pi.matrix_block_apply],
end

lemma inner_left_conj [hÏˆ : Î  i, fact (Ïˆ i).is_faithful_pos_map]
  (x y z : Î  i, matrix (s i) (s i) â„‚) :
  âŸªx, y * zâŸ«_â„‚ = âŸªx * ((module.dual.pi.matrix_block Ïˆ) * (star z)
          * (module.dual.pi.matrix_block Ïˆ)â»Â¹), yâŸ«_â„‚ :=
mul_right (Î» i, (hÏˆ i).elim) _ _ _

lemma inner_right_mul [hÏˆ : Î  i, fact (Ïˆ i).is_faithful_pos_map]
  (x y z : Î  i, matrix (s i) (s i) â„‚) :
  âŸªx, y * zâŸ«_â„‚ = âŸªstar y * x, zâŸ«_â„‚ :=
@linear_functional_left_mul _ _ _ _ _ _ (module.dual.pi Ïˆ) _ _ _

lemma adjoint_eq [hÏˆ : Î  i, fact (Ïˆ i).is_faithful_pos_map] :
  (module.dual.pi Ïˆ).adjoint
    = algebra.linear_map â„‚ (Î  i, matrix (s i) (s i) â„‚) :=
begin
  rw linear_map.ext_iff,
  intros x,
  apply @ext_inner_right â„‚,
  intros y,
  rw [linear_map.adjoint_inner_left, algebra.linear_map_apply],
  simp_rw [inner_pi_eq_sum, pi.algebra_map_apply, algebra_map_eq_smul,
    inner_product_space.core.inner_smul_left, module.dual.is_faithful_pos_map.inner_eq,
    conj_transpose_one, matrix.one_mul, â† finset.mul_sum],
  refl,
end

protected noncomputable def basis (hÏˆ : Î  i, (Ïˆ i).is_faithful_pos_map) :
  basis (Î£ i, s i Ã— s i) â„‚ (Î  i, matrix (s i) (s i) â„‚) :=
pi.basis (Î» i, (hÏˆ i).basis)

protected lemma basis_apply (hÏˆ : Î  i, (Ïˆ i).is_faithful_pos_map)
  (ijk : Î£ i, s i Ã— s i) :
  module.dual.pi.is_faithful_pos_map.basis hÏˆ ijk
    = include_block (std_basis_matrix ijk.2.1 ijk.2.2 1
      â¬ (hÏˆ ijk.1).matrix_is_pos_def.rpow (-(1/2 : â„))) :=
begin
  simp only [module.dual.pi.is_faithful_pos_map.basis, pi.basis_apply, function.funext_iff],
  intros i j k,
  simp only [linear_map.std_basis_apply, pi.mul_apply, include_block_apply,
    mul_eq_mul, mul_apply, dite_apply, mul_dite, mul_zero, pi.zero_apply,
    function.update],
  rw [dite_eq_iff'],
  split,
  { intros h,
    simp only [h, eq_self_iff_true, dif_pos, module.dual.is_faithful_pos_map.basis_apply],
    finish, },
  { intros h,
    rw eq_comm at h,
    simp only [h, not_false_iff, dif_neg], },
end

protected lemma basis_apply' (hÏˆ : Î  i, (Ïˆ i).is_faithful_pos_map)
  (i : k) (j l : s i) :
  module.dual.pi.is_faithful_pos_map.basis hÏˆ âŸ¨i, (j,l)âŸ© =
    include_block (std_basis_matrix j l 1
      â¬ (hÏˆ i).matrix_is_pos_def.rpow (-(1/2 : â„))) :=
module.dual.pi.is_faithful_pos_map.basis_apply hÏˆ _

lemma include_block_left_inner [hÏˆ : Î  i, fact (Ïˆ i).is_faithful_pos_map]
  {i : k} (x : matrix (s i) (s i) â„‚)
  (y : Î  j, matrix (s j) (s j) â„‚) :
  âŸªinclude_block x, yâŸ«_â„‚ = âŸªx, y iâŸ«_â„‚ :=
begin
  simp only [inner_pi_eq_sum, include_block_apply,
    module.dual.is_faithful_pos_map.inner_eq', â† mul_eq_mul,
    â† star_eq_conj_transpose, star_dite, star_zero, mul_dite, mul_zero, dite_mul, zero_mul],
  simp_rw [trace_iff, dite_apply, pi.zero_apply, finset.sum_dite_irrel,
    finset.sum_const_zero, finset.sum_dite_eq, finset.mem_univ, if_true],
  refl,
end

lemma include_block_inner_same [hÏˆ : Î  i, fact (Ïˆ i).is_faithful_pos_map]
  {i : k} {x y : matrix (s i) (s i) â„‚} :
  âŸªinclude_block x, include_block yâŸ«_â„‚ = âŸªx, yâŸ«_â„‚ :=
by rw [include_block_left_inner, include_block_apply_same]

lemma include_block_inner_same' [hÏˆ : Î  i, fact (Ïˆ i).is_faithful_pos_map]
  {i j : k} {x : matrix (s i) (s i) â„‚} {y : matrix (s j) (s j) â„‚} (h : i = j) :
  âŸªinclude_block x, include_block yâŸ«_â„‚ = âŸªx, (by { rw h, exact y, })âŸ«_â„‚ :=
begin
  simp_rw [include_block_left_inner, include_block_apply, h, eq_self_iff_true, dif_pos],
  refl,
end

lemma include_block_inner_ne_same [hÏˆ : Î  i, fact (Ïˆ i).is_faithful_pos_map]
  {i j : k} {x : matrix (s i) (s i) â„‚}
  {y : matrix (s j) (s j) â„‚} (h : i â‰  j) :
  âŸªinclude_block x, include_block yâŸ«_â„‚ = 0 :=
begin
  simp only [include_block_left_inner, include_block_apply_ne_same _ h.symm, inner_zero_right],
end

protected lemma basis.apply_cast_eq_mpr (hÏˆ : Î  i, (Ïˆ i).is_faithful_pos_map)
  {i j : k} {a : s j Ã— s j} (h : i = j) :
  (hÏˆ i).basis (by { rw h, exact a, }) =
  by { rw h, exact (hÏˆ j).basis a } :=
begin
  simp only [eq_mpr_eq_cast, h],
  finish,
end

protected lemma basis_is_orthonormal [hÏˆ : Î  i, fact (Ïˆ i).is_faithful_pos_map] :
  orthonormal â„‚
    (module.dual.pi.is_faithful_pos_map.basis (Î» i, (hÏˆ i).elim)) :=
begin
  rw orthonormal_iff_ite,
  intros i j,
  rw [eq_comm, ite_eq_iff'],
  split,
  { rintros rfl,
    simp only [module.dual.pi.is_faithful_pos_map.basis_apply, include_block_inner_same', cast_eq, eq_mpr_eq_cast,
      â† module.dual.is_faithful_pos_map.basis_apply, orthonormal_iff_ite.mp (module.dual.is_faithful_pos_map.basis_is_orthonormal) i.snd,
      eq_self_iff_true, if_true], },
  { intros h,
    by_cases h' : i.fst = j.fst,
    { rw [sigma.ext_iff, not_and_distrib] at h,
      cases h with h1 h2,
      { contradiction, },
      { rw [â† sigma.eta i, â† sigma.eta j],
        simp only [module.dual.pi.is_faithful_pos_map.basis_apply, include_block_inner_same' h',
          â† module.dual.is_faithful_pos_map.basis_apply, â† basis.apply_cast_eq_mpr (Î» i, (hÏˆ i).elim),
          sigma.eta, orthonormal_iff_ite.mp module.dual.is_faithful_pos_map.basis_is_orthonormal i.snd],
        rw [eq_comm, ite_eq_right_iff],
        intros hh,
        rw hh at h2,
        simp only [eq_mpr_eq_cast, cast_heq, not_true] at h2,
        contradiction, }, },
    { simp only [module.dual.pi.is_faithful_pos_map.basis_apply,
    include_block_inner_ne_same h'], }, },
end

protected noncomputable def orthonormal_basis
  [hÏˆ : Î  i, fact (Ïˆ i).is_faithful_pos_map] :
  orthonormal_basis (Î£ i, (s i) Ã— (s i)) â„‚ (Î  i, matrix (s i) (s i) â„‚) :=
basis.to_orthonormal_basis (module.dual.pi.is_faithful_pos_map.basis (Î» i, (hÏˆ i).elim))
  module.dual.pi.is_faithful_pos_map.basis_is_orthonormal

protected lemma orthonormal_basis_apply [hÏˆ : Î  i, fact (Ïˆ i).is_faithful_pos_map]
  {ijk : Î£ i, s i Ã— s i} :
  (module.dual.pi.is_faithful_pos_map.orthonormal_basis : orthonormal_basis _ _ _) ijk
    = include_block (std_basis_matrix ijk.2.1 ijk.2.2 1
      â¬ (hÏˆ ijk.1).elim.matrix_is_pos_def.rpow (-(1/2 : â„))) :=
begin
  rw [â† module.dual.pi.is_faithful_pos_map.basis_apply _],
  simp only [module.dual.pi.is_faithful_pos_map.orthonormal_basis,
    basis.coe_to_orthonormal_basis],
end

protected lemma orthonormal_basis_apply' [hÏˆ : Î  i, fact (Ïˆ i).is_faithful_pos_map]
  {i : k} {j l : s i} :
  (module.dual.pi.is_faithful_pos_map.orthonormal_basis : orthonormal_basis _ _ _) âŸ¨i, (j,l)âŸ©
    = include_block (std_basis_matrix j l 1
      â¬ (hÏˆ i).elim.matrix_is_pos_def.rpow (-(1/2 : â„))) :=
module.dual.pi.is_faithful_pos_map.orthonormal_basis_apply

protected lemma inner_coord [hÏˆ : Î  i, fact (Ïˆ i).is_faithful_pos_map]
  (ijk : Î£ i, s i Ã— s i) (y : Î  i, matrix (s i) (s i) â„‚) :
  âŸªmodule.dual.pi.is_faithful_pos_map.basis (Î» i, (hÏˆ i).elim) ijk, yâŸ«_â„‚
    = ((y ijk.1) â¬ ((hÏˆ ijk.1).elim.matrix_is_pos_def.rpow (1 / 2))) ijk.2.1 ijk.2.2 :=
begin
  let Q := (Ïˆ ijk.1).matrix,
  let hQ := (hÏˆ ijk.1).elim.matrix_is_pos_def,
  simp_rw [module.dual.pi.is_faithful_pos_map.basis_apply, include_block_left_inner,
    â† module.dual.is_faithful_pos_map.orthonormal_basis_apply,
    module.dual.is_faithful_pos_map.inner_coord],
end

protected lemma basis_repr_apply [hÏˆ : Î  i, fact (Ïˆ i).is_faithful_pos_map]
  (x : Î  i, matrix (s i) (s i) â„‚)
  (ijk : Î£ i, s i Ã— s i) :
  (module.dual.pi.is_faithful_pos_map.basis (Î» i, (hÏˆ i).elim)).repr x ijk
    = âŸª(hÏˆ ijk.1).elim.basis ijk.2, x ijk.1âŸ«_â„‚ :=
begin
  rw [module.dual.is_faithful_pos_map.basis_apply,
    â† module.dual.is_faithful_pos_map.orthonormal_basis_apply,
    â† orthonormal_basis.repr_apply_apply],
  refl,
end

lemma matrix_block.is_self_adjoint [hÏˆ : Î  i, fact (Ïˆ i).is_faithful_pos_map] :
  _root_.is_self_adjoint (module.dual.pi.matrix_block Ïˆ) :=
begin
  ext1,
  simp only [pi.star_apply, module.dual.pi.matrix_block_apply,
    star_eq_conj_transpose, (hÏˆ x).elim.matrix_is_pos_def.1.eq],
end

noncomputable def matrix_block_invertible [hÏˆ : Î  i, fact (Ïˆ i).is_faithful_pos_map] :
  invertible (module.dual.pi.matrix_block Ïˆ) :=
begin
  haveI := Î» i, (hÏˆ i).elim.matrix_is_pos_def.invertible,
  apply invertible.mk (module.dual.pi.matrix_block Ïˆ)â»Â¹,
  all_goals { ext1,
    simp_rw [pi.mul_apply, pi.inv_apply, module.dual.pi.matrix_block_apply,
      mul_eq_mul, pi.one_apply], },
  work_on_goal 1 { rw [inv_mul_of_invertible], },
  rw [mul_inv_of_invertible],
end

lemma matrix_block_inv_mul_self [hÏˆ : Î  i, fact (Ïˆ i).is_faithful_pos_map] :
  (module.dual.pi.matrix_block Ïˆ)â»Â¹
    * (module.dual.pi.matrix_block Ïˆ) = 1 :=
begin
  haveI := Î» i, (hÏˆ i).elim.matrix_is_pos_def.invertible,
  ext1,
  simp_rw [pi.mul_apply, pi.inv_apply, module.dual.pi.matrix_block_apply,
    mul_eq_mul, pi.one_apply, inv_mul_of_invertible],
end
lemma matrix_block_self_mul_inv [hÏˆ : Î  i, fact (Ïˆ i).is_faithful_pos_map] :
  (module.dual.pi.matrix_block Ïˆ)
    * (module.dual.pi.matrix_block Ïˆ)â»Â¹ = 1 :=
begin
  haveI := Î» i, (hÏˆ i).elim.matrix_is_pos_def.invertible,
  ext1,
  simp_rw [pi.mul_apply, pi.inv_apply, module.dual.pi.matrix_block_apply,
    mul_eq_mul, pi.one_apply, mul_inv_of_invertible],
end

noncomputable def to_matrix (hÏˆ : Î  i, (Ïˆ i).is_faithful_pos_map) :
  ((Î  i, matrix (s i) (s i) â„‚) â†’â‚—[â„‚] (Î  i, matrix (s i) (s i) â„‚))
    â‰ƒâ‚[â„‚] matrix (Î£ i, s i Ã— s i) (Î£ i, s i Ã— s i) â„‚ :=
linear_map.to_matrix_alg_equiv
  (module.dual.pi.is_faithful_pos_map.basis hÏˆ)

@[simps] noncomputable def is_block_diagonal_basis (hÏˆ : Î  i, (Ïˆ i).is_faithful_pos_map) :
  basis (Î£ i, s i Ã— s i) â„‚ { x : matrix (Î£ i, s i) (Î£ i, s i) â„‚ // x.is_block_diagonal } :=
{ repr := is_block_diagonal_pi_alg_equiv.to_linear_equiv.trans (module.dual.pi.is_faithful_pos_map.basis hÏˆ).repr }

lemma to_matrix_apply'
  [hÏˆ : Î  i, fact (Ïˆ i).is_faithful_pos_map]
  (f : (Î  i, matrix (s i) (s i) â„‚) â†’â‚—[â„‚] Î  i, matrix (s i) (s i) â„‚)
  (r l : Î£ r, s r Ã— s r) :
  (to_matrix (Î» i, (hÏˆ i).elim)) f r l
    = (f (include_block ((hÏˆ l.1).elim.basis l.2)) r.1
      â¬ (hÏˆ r.1).elim.matrix_is_pos_def.rpow (1 / 2)) r.2.1 r.2.2 :=
begin
  simp_rw [to_matrix, linear_map.to_matrix_alg_equiv_apply,
    is_faithful_pos_map.basis_repr_apply,
    â† module.dual.is_faithful_pos_map.inner_coord,
    is_faithful_pos_map.basis_apply,
    module.dual.is_faithful_pos_map.orthonormal_basis_apply,
    â† module.dual.is_faithful_pos_map.basis_apply],
end

lemma star_alg_equiv_adjoint_eq [hÏˆ : Î  i, fact (Ïˆ i).is_faithful_pos_map]
  (f : Î  i, matrix (s i) (s i) â„‚ â‰ƒâ‹†â‚[â„‚] matrix (s i) (s i) â„‚) (x : Î  i, matrix (s i) (s i) â„‚) :
  (star_alg_equiv.pi f).to_alg_equiv.to_linear_map.adjoint x
    = ((star_alg_equiv.pi f).symm
      (x * module.dual.pi.matrix_block Ïˆ))
        * (module.dual.pi.matrix_block Ïˆ)â»Â¹ :=
begin
  letI := @matrix_block_invertible _ _ _ _ _ _ Ïˆ _,
  letI := Î» i, (hÏˆ i).elim.matrix_is_pos_def.invertible,
  apply @ext_inner_left â„‚,
  intros a,
  simp_rw [linear_map.adjoint_inner_right, alg_equiv.to_linear_map_apply,
    star_alg_equiv.coe_to_alg_equiv],
  rw [â† star_alg_equiv.of_pi_is_inner],
  simp_rw [unitary.inner_aut_star_alg_apply, unitary.inner_aut_star_alg_symm_apply, mul_assoc],
  nth_rewrite_rhs 0 â† mul_assoc (module.dual.pi.matrix_block Ïˆ),
  nth_rewrite_rhs 0 â† mul_assoc,
  rw [inner_left_conj, inner_right_mul],
  simp_rw [star_semigroup.star_mul, is_self_adjoint.star_eq
    matrix_block.is_self_adjoint, mul_assoc],
  have t1 : module.dual.pi.matrix_block Ïˆ * (module.dual.pi.matrix_block Ïˆ)â»Â¹ = 1 :=
  begin
    ext1,
    simp only [pi.mul_apply, pi.inv_apply, mul_eq_mul,
      module.dual.pi.matrix_block_apply, mul_inv_of_invertible, pi.one_apply],
  end,
  have t2 := calc module.dual.pi.matrix_block Ïˆ * star (module.dual.pi.matrix_block Ïˆ)â»Â¹
    = module.dual.pi.matrix_block Ïˆ
      * (module.dual.pi.matrix_block Ïˆ)â»Â¹ :
  by { congr,
    simp only [pi.inv_def, pi.star_def, module.dual.pi.matrix_block_apply,
      star_eq_conj_transpose, (hÏˆ _).elim.matrix_is_pos_def.1.eq,
      (hÏˆ _).elim.matrix_is_pos_def.inv.1.eq], }
  ... = 1 : t1,
  simp_rw [t1, â† mul_assoc (module.dual.pi.matrix_block Ïˆ), t2, mul_one, one_mul,
    unitary.coe_star, star_star],
end

private lemma mul_inv_eq_iff_eq_mul_aux [hÏˆ : Î  i, fact (Ïˆ i).is_faithful_pos_map]
  (b c : Î  i, matrix (s i) (s i) â„‚) :
  b * (module.dual.pi.matrix_block Ïˆ)â»Â¹ = c â†” b = c * (module.dual.pi.matrix_block Ïˆ) :=
begin
  split; rintros rfl; rw [mul_assoc],
  { rw [matrix_block_inv_mul_self, mul_one], },
  { rw [matrix_block_self_mul_inv, mul_one], },
end

lemma star_alg_equiv_commute_iff [hÏˆ : Î  i, fact (Ïˆ i).is_faithful_pos_map]
  (f : Î  i, matrix (s i) (s i) â„‚ â‰ƒâ‹†â‚[â„‚] matrix (s i) (s i) â„‚) :
  commute (module.dual.pi.matrix_block Ïˆ) (Î» i, (star_alg_equiv.pi.unitary f i))
    â†” star_alg_equiv.pi f (module.dual.pi.matrix_block Ïˆ) = module.dual.pi.matrix_block Ïˆ :=
begin
  nth_rewrite_rhs 0 [â† star_alg_equiv.of_pi_is_inner],
  rw [unitary.inner_aut_star_alg_apply,
    unitary.coe_star],
  rw [unitary.inj_mul (unitary.pi (star_alg_equiv.pi.unitary f))],
  simp_rw [mul_assoc, unitary.coe_star_mul_self, mul_one],
  rw [eq_comm, commute, semiconj_by],
  refl,
end

lemma star_alg_equiv_is_isometry_tfae [hÏˆ : Î  i, fact (Ïˆ i).is_faithful_pos_map]
  [Î  i, nontrivial (s i)] (f : Î  i, matrix (s i) (s i) â„‚ â‰ƒâ‹†â‚[â„‚] matrix (s i) (s i) â„‚) :
  tfae [(star_alg_equiv.pi f) (module.dual.pi.matrix_block Ïˆ)
    = module.dual.pi.matrix_block Ïˆ,
      (star_alg_equiv.pi f).to_alg_equiv.to_linear_map.adjoint = (star_alg_equiv.pi f).symm.to_alg_equiv.to_linear_map,
    (module.dual.pi Ïˆ) âˆ˜â‚— (star_alg_equiv.pi f).to_alg_equiv.to_linear_map = module.dual.pi Ïˆ,
    âˆ€ x y, âŸª(star_alg_equiv.pi f) x, (star_alg_equiv.pi f) yâŸ«_â„‚ = âŸªx, yâŸ«_â„‚,
    âˆ€ x : Î  i, matrix (s i) (s i) â„‚, â€–(star_alg_equiv.pi f) xâ€– = â€–xâ€–,
    commute (module.dual.pi.matrix_block Ïˆ) (Î» i, (star_alg_equiv.pi.unitary f i))] :=
begin
  tfae_have : 5 â†” 2,
  { simp_rw [inner_product_space.core.norm_eq_sqrt_inner,
    real.sqrt_inj inner_product_space.core.inner_self_nonneg
      inner_product_space.core.inner_self_nonneg, â† complex.of_real_inj,
    inner_self_re, â† @sub_eq_zero _ _ _ âŸª_, _âŸ«_â„‚],
    have : âˆ€ x y, âŸª(star_alg_equiv.pi f) x, (star_alg_equiv.pi f) yâŸ«_â„‚ - âŸªx, yâŸ«_â„‚
      = âŸª((star_alg_equiv.pi f).to_alg_equiv.to_linear_map.adjoint âˆ˜â‚— (star_alg_equiv.pi f).to_alg_equiv.to_linear_map - 1) x, yâŸ«_â„‚,
    { intros x y,
      simp only [linear_map.sub_apply, linear_map.one_apply, inner_sub_left,
        linear_map.comp_apply, linear_map.adjoint_inner_left, star_alg_equiv.coe_to_alg_equiv,
        alg_equiv.to_linear_map_apply], },
    simp_rw [this, inner_map_self_eq_zero, sub_eq_zero, star_alg_equiv.comp_eq_iff,
      linear_map.one_comp], },
  rw tfae_5_iff_2,
  tfae_have : 4 â†” 3,
  { simp_rw [inner_eq, â† map_star (star_alg_equiv.pi f),
      â† _root_.map_mul (star_alg_equiv.pi f), linear_map.ext_iff, linear_map.comp_apply, alg_equiv.to_linear_map_apply,
      star_alg_equiv.coe_to_alg_equiv],
    refine âŸ¨Î» h x, _, Î» h x y, h _âŸ©,
    rw [â† one_mul x, â† star_one],
    exact h _ _, },
  rw tfae_4_iff_3,
  letI := @matrix_block_invertible _ _ _ _ _ _ Ïˆ _,
  simp_rw [linear_map.ext_iff, star_alg_equiv_adjoint_eq f, linear_map.comp_apply,
    alg_equiv.to_linear_map_apply, star_alg_equiv.coe_to_alg_equiv,
    mul_inv_eq_iff_eq_mul_aux,
    module.dual.pi.apply'',
    star_alg_equiv.symm_apply_eq, _root_.map_mul,
    star_alg_equiv.apply_symm_apply,
    pi.forall_left_mul, @eq_comm _ (module.dual.pi.matrix_block Ïˆ),
    â† block_diagonal'_alg_hom_apply, â† _root_.map_mul],
  tfae_have : 1 â†” 2,
  { rw iff_self, trivial, },
  tfae_have : 1 â†’ 3,
  { intros i x,
    nth_rewrite 0 â† i,
    simp_rw [â† _root_.map_mul, star_alg_equiv.pi_is_trace_preserving], },
  tfae_have : 3 â†’ 1,
  { intros i,
    simp_rw [â† star_alg_equiv.pi_is_trace_preserving
      (Î» i, (f i).symm) (module.dual.pi.matrix_block Ïˆ * ((star_alg_equiv.pi f) _)),
      _root_.map_mul, star_alg_equiv.pi_symm_apply_apply, block_diagonal'_alg_hom_apply,
      â† module.dual.pi.apply'',
      @eq_comm _ _ (module.dual.pi Ïˆ _)] at i,
    have := module.dual.pi.apply_eq_of Ïˆ _ i,
    rw [star_alg_equiv.pi_symm_apply_eq] at this,
    exact this.symm, },
  tfae_have : 5 â†” 6,
  { rw [star_alg_equiv_commute_iff], },
  tfae_finish,
end

end module.dual.pi.is_faithful_pos_map

end direct_sum
